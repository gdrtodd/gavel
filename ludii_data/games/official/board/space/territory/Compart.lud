(game "Compart"  
    (players 2) 
    (equipment 
        { 
        (board <Board> use:Vertex) 
        (piece "Marker" Each)
        }
    )
    (rules 
        (meta (swap))
        (play 
            (move Add
                (to
                    (sites Empty)
                    if:(not (is In (to) (sites (values Remembered))))
                    (apply (addScore Mover 1))
                )
                (then
                    (do
                        (forEach Site
                            (sites Group 
                                at:(last To)
                                if:(not (is Enemy (who at:(to))))
                            )
                            (remember Value (site))
                        )
                        next:(if
                            (< 0 
                                (count Sites
                                    in:(difference
                                        (sites Empty)
                                        (sites (values Remembered))
                                    )
                                )
                            )
                            (moveAgain)
                            (forget Value All)
                        )
                    )
                )
            )
        )
        (end
            (if 
                (is Full) 
                {
                (if
                    (=
                        (score P1)
                        (score P2)
                    )
                    (result Mover Win)
                )
                (if 
                    True
                    (byScore misere:True)
                )
                }
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "3x3"      <(square 3)>     "A 3x3 board is currently selected.")
    (item "4x4"      <(square 4)>     "A 4x4 board is currently selected.")
    (item "5x5"      <(square 5)>     "A 5x5 board is currently selected.")
    (item "6x6"      <(square 6)>     "A 6x6 board is currently selected.")
    (item "7x7"      <(square 7)>     "A 7x7 board is currently selected.")
    (item "8x8"      <(square 8)>     "An 8x8 board is currently selected.")
    (item "9x9"      <(square 9)>     "A 9x9 board is currently selected.")
    (item "10x10"    <(square 10)>    "A 10x10 board is currently selected.")   
    (item "11x11"    <(square 11)>    "An 11x11 board is currently selected.")*
    (item "12x12"    <(square 12)>    "A 12x12 board is currently selected.")   
    (item "13x13"    <(square 13)>    "A 13x13 board is currently selected.")
    (item "14x14"    <(square 14)>    "A 14x14 board is currently selected.")   
    (item "15x15"    <(square 15)>    "A 15x15 board is currently selected.")
    (item "16x16"    <(square 16)>    "A 16x16 board is currently selected.")
    (item "17x17"    <(square 17)>    "A 17x17 board is currently selected.")   
    (item "18x18"    <(square 18)>    "An 18x18 board is currently selected.")   
    (item "19x19"    <(square 19)>    "A 19x19 board is currently selected.")
})

(metadata
    (info
        {
        (description "Compart is a simple game of territory and timing. Walling off areas of the board with your stones multiplies enemy placements, but be mindful that your opponent will try to use those extra placements to enclose new areas that multiply your own placements. Whoever has fewer stones at the end wins.")
        (rules "Played with black and white stones on an initially empty square board. On your turn, for every viable area with one or more empty points, place a stone of your color on one of those points.
            
            A viable area is a viable point along with all other such points one can reach from it through a series of steps onto orthogonally adjacent viable points. A viable point is a point that is empty or occupied by a friendly stone.
            
            When the board is full, the player with fewer stones on the board wins. If there is a tie, whoever placed the last stone wins. Ties are only possible on even-sized boards.
            
        To make the game fair, the second player will have the option, on their first turn only, to swap sides with their opponent instead of making a regular move.")
        (id "3978")
        (version "1.3.11")
        (classification "board/space/territory")
        (author "Luis Bolanos Mures")
        (credit "Michael Amundsen")
        (date "2023")
        }
    )
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
        (board Colour Phase0 (colour 250 210 148))
        (board Colour Phase1 (colour 250 210 148))
        (board Style Board)
    })
)
