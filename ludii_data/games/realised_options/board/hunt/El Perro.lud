(game "El Perro"
    (players 2) 
    (equipment { 
        ("AlquerqueBoard" 5 5)
        (piece "Goat" P2
            ("StepToEmpty" 
                (if (= (value Player Mover) 1)
                    (directions {S SE SW E W})
                    (directions {N NE NW E W})
                )
            )
        )
        (piece "Dog" P1 
            (or 
                ("HopCapture"
                    (from (from))
                    Adjacent
                    (then (if (can Move ("HopCapture" (from (last To)))) (moveAgain))) 
                )
                ("StepToEmpty")
            )
        )
    }) 
    
    (rules 
        (start {
            (place "Goat2" (union (intersection (union (sites Left) (sites Right)) (sites Row 2)) (expand (sites Bottom))))
            (place "Dog1" (sites Centre))
        })
        (play
            (if ("SameTurn")
                (or
                    ("HopCapture"
                        (from (last To))
                        Adjacent
                        (then (if (can Move ("HopCapture" (from (last To)))) (moveAgain))) 
                    )
                    (move Pass)
                )
                (forEach Piece)
                (then
                    (if (not 
                            (can Move
                                (forEach Piece 
                                    ("StepToEmpty" 
                                        (if (= (value Player P2) 1)
                                            (directions {S SE SW})
                                            (directions {N NE NW})
                                        )	
                                    )
                                    P2
                                )
                            )
                        )
                        (set Value P2 (if (= (value Player P2) 1) 0 1))
                    )
                )
            )
        )
        
        (end ("NoMovesP1NoPiecesP2")) 
    )
)

