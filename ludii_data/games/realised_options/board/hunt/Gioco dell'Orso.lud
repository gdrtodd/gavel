(game "Gioco dell'Orso"
    (players 2) 
    (equipment { 
        (board
            (add
                (concentric {1 4 12})
                edges:{{6 8} {9 11} {12 14} {5 15}}
            )
            use:Vertex
        )
        (piece "Human" Each ("StepToEmpty"))
        (piece "Bear" Each ("StepToEmpty"))
        (hand Each)
    }) 
    
    (rules 
        (start {
            (place "Human1" (handSite P1) count:3)
            (place "Bear2" (sites Centre))
        })	
        
        phases:{
        (phase "PlacementP1" 
            (play
                (move 
                    (from (handSite P1))
                    (to (intersection (sites Empty) (sites {1..4})))
                    (then (if ("HandOccupied" P1) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P1) "HuntingP2")
        )
        (phase "HuntingP2" 
            (play 
                (forEach Piece
                    (then 
                        (if (not 
                                (can Move 
                                    ("StepToEmpty" (from (where "Bear" P2)))
                                )
                            )
                            (and {
                                (addScore P2 (- (count Moves) 3))
                                (remove (sites Occupied by:All container:"Board"))
                                (add (piece "Bear1") (to (sites Centre)))
                                (add (piece "Human2") (to (handSite P2)) count:3)
                            })
                        )
                    )
                )
            )
            (nextPhase (= 1 (count Sites in:(sites Occupied by:All container:"Board"))) "PlacementP2")
        )
        (phase "PlacementP2" 
            (play
                (move 
                    (from (handSite P2))
                    (to (intersection (sites Empty) (sites {1..4})))
                    (then (if ("HandOccupied" P2) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P2) "HuntingP1")
        )
        (phase "HuntingP1" 
            (play 
                (forEach Piece
                    (then 
                        (if (not 
                                (can Move 
                                    ("StepToEmpty" (from (where "Bear" P1)))
                                )
                            )
                            (addScore P1 (- (count Moves) (+ (score P2) 6)))
                        )
                    )
                )
            )
            (end (if (!= 0 (score P1)) (byScore)))
        )
        }
        
    )		
)

