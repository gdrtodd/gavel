(define "BigHop" 
    (move Hop 
        (between 
            (range 1 9)
            if:#1
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
    )
)

(game "Merimueng-rimueng" 
    (players 2) 
    (equipment { 
        ("AlquerqueBoardWithBottomAndTopTriangles")
        (hand P2) 
        (piece "Tiger" P1 
            (or {
                ("StepToEmpty")
                ("BigHop" ("IsEnemyAt" (between))) 
            })
        ) 
        (piece "Sheep" P2 "StepToEmpty") 
    }) 
    (rules 
        (start {
            (place "Sheep2" (difference (expand (sites Centre)) (centrePoint)))
            (place Stack "Sheep2" (handSite P2) count:15)
            (place Stack "Tiger1" (sites Centre) counts:{2})
        })
        
        phases:{
        (phase "Movement"
            (play (forEach Piece top:True))
            
            (end ("NoMovesP1NoPiecesP2"))
            
            (nextPhase 
                (and 
                    ("HandOccupied" P2) 
                    (< (count Sites in:(sites Occupied by:P2 container:"Board")) 8)
                ) 
                "Placement"
            )
        )
        
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                    (then (moveAgain))
                ) 
            )
            (nextPhase 
                (or 
                    ("HandEmpty" P2) 
                    (= (count Sites in:(sites Occupied by:P2 container:"Board")) 8)
                ) 
                "Movement"
            )
        )
        }
    )
)

