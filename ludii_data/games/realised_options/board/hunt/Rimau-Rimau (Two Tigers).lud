(game "Rimau-Rimau (Two Tigers)" 
    (players 2) 
    (equipment { 
        ("AlquerqueBoardWithBottomAndTopTriangles")
        (hand Each) 
        (piece "Tiger" P1 
            (or {
                ("StepToEmpty")
                ("HopCapture") 
            })
        ) 
        (piece "human" P2 "StepToEmpty") 
    }) 
    (rules 
        
        (start {
            (place "human2" (difference (expand (sites Centre)) (centrePoint)))
            (place "human2" (handSite P2) count:16)
            (place "Tiger1" (handSite P1) count:2)
        })
        
        phases:{
        
        (phase "PlacementFirstTiger" P1
            (play (move (from (handSite P1)) (to (sites Empty)) (then (moveAgain))))
            (nextPhase Mover "RemoveHuman")
        )
        
        (phase "RemoveHuman" P1
            (play 
                (move
                    Remove
                    (intersection (sites Board) (sites Occupied by:Next))
                    (then (moveAgain))
                )
            )
            (nextPhase Mover "PlacementSecondTiger")
        )
        
        (phase "PlacementSecondTiger" P1
            (play (move (from (handSite P1)) (to (sites Empty))))
            (nextPhase Mover "Movement")
        )
        
        (phase "PlacementHuman" P2
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                ) 
            )
            (nextPhase 
                ("HandEmpty" P2) 
                "Movement"
            )
        )
        
        ("PhaseMovePiece" "Movement"
            (end {
                (if (no Moves P1) (result P2 Win))
                (if (no Pieces P2) (result P1 Win))
            })
        )
        }
        
    )
)

