(game "Sher Bakr" 
    (players 2) 
    (equipment { 
        (board
            (rotate 180 
                (add
                    (remove 
                        (scale 1 2 (wedge 3)) 
                        vertices:{2}
                    )
                    vertices:{{2 1.333}}
                    edges:{{4 6} {6 0} {1 6} {6 2}}
                )
            )
            use:Vertex
        )
        (hand Each) 
        (piece "Goat" P1
            (move Step 
                (to 
                    if:(or 
                        (is Empty (to))
                        (and (= (to) (centrePoint)) ("IsFriendAt" (to)))
                    )
                )
            )
        )
        (piece "Tiger" P2
            (or {
                ("StepToEmpty")
                ("HopCapture")
                
                (if (and {(= (from) 2) ("IsEnemyAt" (centrePoint)) (is Empty 1)})
                    (move (from 2) (to 1) (then (remove (centrePoint))))
                )
                (if (and {(= (from) 1) ("IsEnemyAt" (centrePoint)) (is Empty 2)})
                    (move (from 1) (to 2) (then (remove (centrePoint))))
                )
            })
        )
    }) 
    (rules 
        (start {
            (place Stack "Goat1" (centrePoint) count:4)
            (place Stack "Tiger2" 0)
        })
        
        (play (forEach Piece top:True))
        
        (end {
            (if (no Moves P2) (result P1 Win))
            (if (= 3 (count Pieces P1)) (result P2 Win))
        })
    )
)

