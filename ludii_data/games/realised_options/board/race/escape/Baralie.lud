(define "NextSiteFrom" ("NextSiteOnTrack" (pips) from:#1))

(define "NextSiteFromDist6" ("NextSiteOnTrack" 6 from:#1))

(define "RemoveAPiece" (move Remove (site)))

(define "Bar" (mapEntry (mover)))

(define "AllPieceEscaped" (no Pieces Mover))

(define "CanEscape" ("IsOffBoard" #1))

(define "MoveTo"
    (forEach Site
        (sites Occupied by:Mover)
        (if ("CanEscape" #1) 
            ("RemoveAPiece")
            (move 
                (from (site))
                (to #1
                    if:("NoEnemyOrOnlyOne" (to))
                    ("HittingCapture" ("Bar")) 
                )
            )
        )
    )		
)

(game "Baralie" 
    (players 2) 
    (equipment { 
        ("BackgammonBoard" "BackgammonTracksSameDirectionWithBar") 
        (dice num:2)
        (map {(pair 1 19) (pair 2 6)}) 
        (piece "Disc" Each) 
    })
    
    (rules
        (start { 
            (place Stack "Disc1" 12 count:15)
            (place Stack "Disc2" 11 count:14) 
            (place Stack "Disc2" 10)
        })
        
        (play 
            ("RollEachNewTurnMove"
                (if (all DiceUsed)
                    ("MoveTo" ("NextSiteFromDist6" (site)))
                    (forEach Die
                        if:("DieNotUsed")
                        ("MoveTo" ("NextSiteFrom" (site)))
                        (then (moveAgain))
                    )
                )
            )
        )
        
        (end 
            (if ("AllPieceEscaped")
                (result Mover Win)
            )
        )
    )
)

