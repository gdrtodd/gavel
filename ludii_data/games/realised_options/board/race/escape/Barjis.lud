(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "EnterAPiece" 
    (move
        (from (handSite Mover))
        (to (centrePoint)
            if:(not ("IsFriendAt" (to)))
            ("HittingCapture" (handSite (who at:(to)))) 
        )
    )
)

(define "IsSpecialThrow" (is In ("ThrowValue") (array {10 25})))

(game "Barjis" 
    (players 4) 
    (equipment { 
        ("PachisiBoard" 
            {
            (track "Track1" "96,9,S,E1,N,11,E,N,W,53,N,W,S,42,W,S,E,8,S,E1,N8" P1 directed:True)
            (track "Track2" "96,26,W,S,E,8,S,E,N,11,E,N,W,53,N,W,S,42,W,S1,E8" P2 directed:True)
            (track "Track3" "96,52,N,W,S,42,W,S,E,8,S,E,N,11,E,N,W,53,N,W1,S8" P3 directed:True)
            (track "Track4" "96,27,E,N,W,53,N,W,S,42,W,S,E,8,S,E,N,11,E,N1,W8" P4 directed:True)
            } 
        )
        (hand Each) 
        ("StickDice" 6)
        (piece "Pawn" Each
            (if ("IsOffBoard" ("NextSiteOnTrack" ("ThrowValue")))
                (move Remove (from))
                (move 
                    (from (from))
                    (to 
                        ("NextSiteOnTrack" ("ThrowValue"))
                        if:(or 
                            (is Empty (to))
                            (and 
                                ("IsEnemyAt" (to))
                                (not (is In (to) (sites "SafeSites")))	
                            )
                        )
                        ("HittingCapture" (handSite (who at:(to)))) 
                    )
                )	
            )
        )
        (map "Throw" {(pair 0 6) (pair 1 10) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 25) (pair 6 12)})
        (regions "SafeSites" (sites {60 62 16 48 87 89 37 2}))
    }) 
    (rules 
        (start (place "Pawn" "Hand" count:4))
        (play 
            ("RollMove" 
                (or 
                    (forEach Piece) 
                    (if (and ("IsSpecialThrow") (is Occupied (handSite Mover)))
                        ("EnterAPiece")
                    )
                )
            )
        )
        
        (end ("EscapeWin"))
    )
)

