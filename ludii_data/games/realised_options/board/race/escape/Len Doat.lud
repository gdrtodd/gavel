(define "ThrowValue" (value Player Mover))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" ("ThrowValue")))

(define "RemoveAPiece" (move Remove (from)))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to))
        (fromTo 
            (from (to)) 
            (to (handSite Next))
            count:(count at:(to))
        )
    ) 
)

(define "HasToThrow" (< ("ThrowValue") 0))

(define "StopTurnIfNoMoveAgain" (if (!= (value Player Mover) -2) (set Value Mover -1)))
(define "ThrowAgainAndPlay" (and (moveAgain) (set Value Mover -2)))
(define "IfMoveAgain" (= (value Player Mover) -2))

(define "Move" 
    (if (and (= "ThrowValue" 1) ("HandOccupied" Mover))
        (move (from (handSite Mover))
            (to (mapEntry "EntrySite" (mover)))
            (then ("ThrowAgainAndPlay"))
        )
        (if (and (= "ThrowValue" 5) ("HandOccupied" Mover))
            (move (from (handSite Mover))
                (to (mapEntry "EntrySite" (mover)))
                count:(count Cell at:(handSite Mover))
                (then ("ThrowAgainAndPlay"))
            )
            (forEach Piece)
        )
        (then ("StopTurnIfNoMoveAgain"))
    )
)

(define "ThrowMove" 
    (if ("HasToThrow")
        ("RollMove"
            (if ("IfMoveAgain")
                (do (set Value Mover (count Pips)) next:("Move"))
                (move Pass (then (set Value Mover (count Pips))))
            )
        )
        ("Move")
    )
)

(game "Len Doat"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 3 5)
                (shift 7.5 0 (rectangle 3 5))
                (shift 0 3.5 (rectangle 3 5))
                (shift 7.5 3.5 (rectangle 3 5))
            })
            { 
            (track "Track1" "20,E,N,W,14,W,S,E,44,W,S,E,45,E,N,W,15,E,N1,W,End" P1 directed:True)
            (track "Track2" "39,W,S,E,45,E,N,W,15,E,N,W,14,W,S,E,44,W,S1,E,End" P2 directed:True)
            }
        )
        ("StickDice" 5)
        (hand Each)
        (piece "Marker" Each
            (if ("IsEndTrack" "SiteToMoveOnTrack")
                "RemoveAPiece"
                (if ("IsNotOffBoard" "SiteToMoveOnTrack") 	 			
                    (if (if
                            (not ("IsEnemyAt" "SiteToMoveOnTrack"))
                            True
                            (not (is In "SiteToMoveOnTrack" (sites "Protect")))
                        )
                        (move 
                            (from) 
                            (to 
                                "SiteToMoveOnTrack" 
                                "CaptureEnemyPiece"
                            )
                            (then 
                                (if 
                                    (or (= 1 (value Player Mover)) (= 5 (value Player Mover)))
                                    ("ThrowAgainAndPlay")
                                ) 
                            )
                        )
                    )
                )
            )
        )
        (map "EntrySite" {(pair P1 (coord "F2")) (pair P2 (coord "E5"))})
        (regions "Protect" (sites {"A1" "A3" "A4" "A6" "J1" "J3" "J4" "J6"}))
    })
    (rules 
        (start (place "Marker" "Hand" count:3))
        (play 
            (if (can Move ("ThrowMove"))
                ("ThrowMove")
                (move Pass 
                    (then 
                        (and 
                            (if 
                                (or (= 1 (value Player Mover)) (= 5 (value Player Mover)))
                                ("ThrowAgainAndPlay")
                            ) 
                            ("StopTurnIfNoMoveAgain")
                        )
                    )
                )
            )
        )
        (end ("EscapeWin"))
    )
)

