(define "NextSiteFrom" ("NextSiteOnTrack" #2 from:#1))

(define "NotEnemyOrOnlyOne" 
    (or 
        (and {
            ("IsEnemyAt" (to)) 
            (not (is In (to) (sites Mover "OppositeSide")))
            ("IsEmptyOrSingletonStack" (to))
        }) 
        (not ("IsEnemyAt" (to)))
    )
)

(define "AllPiecesInFinalQuadrant" ("AllOwnedPiecesIn" (sites Mover "FinalQuadrant")))

(define "SpecialWin" 
    (if (is Mover P1)
        (and {
            (= 15 (count Pieces Mover in:(sites {0..5})))
            ("IsFriendAt" 3)
            ("IsFriendAt" 4)
            ("IsFriendAt" 5)
            (= 3 (size Stack at:0))
            (= 3 (size Stack at:1))
            (= 3 (size Stack at:2))
        })
        (and {
            (= 15 (count Pieces Mover in:(sites {13..18})))
            ("IsFriendAt" 16)
            ("IsFriendAt" 17)
            ("IsFriendAt" 18)
            (= 3 (size Stack at:13))
            (= 3 (size Stack at:14))
            (= 3 (size Stack at:15))
        })
    )
)

(game "Schuster"
    (players 2)
    (equipment {
        ("BackgammonBoard" ("BackgammonTracksWithBar" End)) 
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if ("IsEndTrack" ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NotEnemyOrOnlyOne")
                            ("HittingCapture" (mapEntry "Bar" Next))
                        )
                    )
                )
                (then 
                    (and
                        ("ReplayNotAllDiceUsed")
                        (if ("SpecialWin")
                            (addScore Mover 2)
                        )
                    )
                )
            )		
        )
        (regions "FinalQuadrant" P1 (sites {20..25}))
        (regions "FinalQuadrant" P2 (sites {7..12}))
        (regions "OppositeSide" P1 (sites {25..20 18..13}))
        (regions "OppositeSide" P2 (sites {0..5 7..12}))
        (map "Bar" {(pair P1 6) (pair P2 19)})
    })
    (rules 
        (start { 
            (place Stack "Disc1" 12 count:15)
            (place Stack "Disc2" 25 count:15) 
        })
        (play ("RollEachNewTurnMove" (forEach Piece top:True)))
        
        (end {
            (if ("SpecialWin") (result Mover Win))
            ("EscapeWin")
        })
    )
)

