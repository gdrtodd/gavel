(define "NextSiteFrom" ("NextSiteOnTrack" #2 from:#1))

(define "AllPiecesInFinalQuadrant" ("AllOwnedPiecesIn" (sites Mover "FinalQuadrant")))

(define "FiveOnThreeFinal" 
    (if (is Mover P1)
        (and {
            ("IsFriendAt" 10)
            ("IsFriendAt" 11)
            ("IsFriendAt" 12)
            (= 5 (size Stack at:10))
            (= 5 (size Stack at:11))
            (= 5 (size Stack at:12))
        })
        (and {
            ("IsFriendAt" 13)
            ("IsFriendAt" 14)
            ("IsFriendAt" 15)
            (= 5 (size Stack at:13))
            (= 5 (size Stack at:14))
            (= 5 (size Stack at:15))
        })
    )
)

(define "ThreeOnFiveFinal" 
    (if (is Mover P1)
        (and {
            ("IsFriendAt" 8)
            ("IsFriendAt" 9)
            ("IsFriendAt" 10)
            ("IsFriendAt" 11)
            ("IsFriendAt" 12)
            (= 3 (size Stack at:8))
            (= 3 (size Stack at:9))
            (= 3 (size Stack at:10))
            (= 3 (size Stack at:11))
            (= 3 (size Stack at:12))
        })
        (and {
            ("IsFriendAt" 13)
            ("IsFriendAt" 14)
            ("IsFriendAt" 15)
            ("IsFriendAt" 17)
            ("IsFriendAt" 18)
            (= 3 (size Stack at:13))
            (= 3 (size Stack at:14))
            (= 3 (size Stack at:15))
            (= 3 (size Stack at:17))
            (= 3 (size Stack at:18))
        })
    )
)

(define "SevenFiveThreeFinal" 
    (if (is Mover P1)
        (and {
            ("IsFriendAt" 10)
            ("IsFriendAt" 11)
            ("IsFriendAt" 12)
            (= 3 (size Stack at:10))
            (= 5 (size Stack at:11))
            (= 7 (size Stack at:12))
        })
        (and {
            ("IsFriendAt" 13)
            ("IsFriendAt" 14)
            ("IsFriendAt" 15)
            (= 7 (size Stack at:13))
            (= 5 (size Stack at:14))
            (= 3 (size Stack at:15))
        })
    )
)

(define "FifteenFinal" 
    (if (is Mover P1)
        (and 
            ("IsFriendAt" 12)
            (= 15 (size Stack at:12))
        )
        (and 
            ("IsFriendAt" 13)
            (= 15 (size Stack at:13))
        )
    )
)

(game "Svensk Bradspel"
    (players 2)
    (equipment {
        ("BackgammonBoard" ("BackgammonTracksSameDirectionOppositeCornersWithBars2" End)) 
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                replayDouble:True 
                if:("DieNotUsed")
                (if ("IsEndTrack" ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:(and 
                                ("NoEnemyOrOnlyOne" (to))
                                (if (not (is In (to) (sites Mover "StartingSide")))
                                    True
                                    (if (= (value Player Mover) 1) True (is Empty (to)))
                                )
                            )
                            (apply 
                                (if ("IsEnemyAt" (to))
                                    (fromTo 
                                        (from (to))
                                        (to (mapEntry "Bar" Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then 
                    (and
                        ("ReplayNotAllDiceUsed")
                        (if (and
                                (= (last To) (mapEntry "12thPoint" Mover))
                                (!= 1 (value Player Mover))
                            )
                            (set Value Mover 1)
                        )
                    )
                )
            )		
        )
        (regions "FinalQuadrant" P1 (sites {7..12}))
        (regions "FinalQuadrant" P2 (sites {13..18}))
        (regions "StartingSide" P1 (sites {25..20 18..14}))
        (regions "StartingSide" P2 (sites {0..5 7..11}))
        (map "12thPoint" {(pair P1 13) (pair P2 12)})
        (map "Bar" {(pair P1 6) (pair P2 19)})
    })
    (rules 
        (start { 
            (place Stack "Disc1" 25 count:15)
            (place Stack "Disc2" 0 count:15) 
        })
        (play 
            ("RollEachNewTurnMove"
                (forEach Piece top:True)
            )		
        )
        
        (end { 
            ("EscapeWin")
            (if ("FiveOnThreeFinal" Mover) (result Mover Win))
            (if ("ThreeOnFiveFinal" Mover) (result Mover Win))
            (if ("SevenFiveThreeFinal" Mover) (result Mover Win))
            (if ("FifteenFinal" Mover) (result Mover Win))
        })
    )
)

