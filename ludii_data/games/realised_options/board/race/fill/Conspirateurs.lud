(define "Hop" 
    (move Hop
        (from #1)
        (between if:(is Occupied (between)))
        (to if:("IsEmptyAndNotVisited" (to)))
        #2
    )
)

(define "HopSequence" 
    ("Hop" 
        #1 
        (then 
            (if 
                (and 
                    (not (is In (last To) (sites "Sanctuaries"))) 
                    (can Move ("Hop" (last To)))
                ) 
                (moveAgain) 
            )
        )
    )
)

(define "GoalReached" ("AllOwnedPiecesIn" (sites "Sanctuaries")))

(game "Conspirateurs" 
    (players 2) 
    (equipment { 
        (board (square 17)) 
        (piece "Pawn" Each)
        (hand Each)
        (regions "Secret Meeting Place" (sites {106..114 123..131 140..148 157..165 174..182}))
        (regions "Sanctuaries" (sites {"A1" "B1" "D1" "F1" "H1" "J1" "L1" "N1" "P1" "Q1" "A2" "Q2" "A4" "Q4" "A6" "Q6" "A8" "Q8" "A9" "Q9" "A10" "Q10" "A12" "Q12" "A14" "Q14" "A16" "Q16" "A17" "B17" "D17" "F17" "H17" "I17" "J17" "L17" "L17" "N17" "P17" "Q17"}))
    }) 
    (rules 
        (start { 
            (place "Pawn1" (handSite P1) count:21)
            (place "Pawn2" (handSite P2) count:21)
        })
        
        phases:{
        (phase "Placement" 
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (forEach (sites "Secret Meeting Place") if:(is Empty (site))))
                )
            ) 
            (nextPhase (and ("HandEmpty" P1) ("HandEmpty" P2)) "Movement")
        )
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    (or 
                        ("HopSequence" (last To)) 
                        (move Pass)
                    )
                    (forEach Piece (if (not (is In (from) (sites "Sanctuaries"))) 
                            (or 
                                ("HopSequence" (from)) 
                                "StepToEmpty"
                            )
                        )
                    )
                )
            )
            (end (if "GoalReached" (result Mover Win)))
        ) 
        }
        
    )
)

