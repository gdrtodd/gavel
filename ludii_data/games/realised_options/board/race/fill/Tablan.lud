(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "IsSpecialThrow" (is In ("ThrowValue") (sites {2 8 12})))

(define "ThrowToActivate" (= 2 ("ThrowValue")))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #1))

(define "ActivatedPiece" (= (state at:#1) 1))

(define "ActivePiece" (set State at:#1 1))

(define "CaptureMove"
    (if ("IsEnemyAt" (to))
        (remove (to))
    )
)

(define "PlayedHalfThrow" (= 1 (var)))

(define "PiecesOwnedOnEnemyHomedBy"
    (count Sites in:(forEach (sites #1 "Home") if:(= (who at:(site)) #2)))
)

(define "NumPieceNotInEnemyHome"
    (count Sites in:(forEach (sites Occupied by:#1) if:(not (is In (site) (sites #2 "Home")))))
)

(game "Tablan"
    (players 2)
    (equipment {
        (board (rectangle 4 12) 
            {
            (track "Track1" "0,E,N1,W,N1,E,N1,W" P1 directed:True)
            (track "Track2" "47,W,S1,E,S1,W,S1,E" P2 directed:True)
            }
            use:Vertex
        )
        (piece "Stick" Each
            (if (and (not (is In (from) (sites Next "Home"))) (!= 0 ("ThrowValue")))
                (or
                    (if (not ("PlayedHalfThrow"))
                        (move
                            (from 
                                (from)
                                if:(if ("ActivatedPiece" (from))
                                    True
                                    ("ThrowToActivate")
                                )	
                            )
                            (to 
                                ("SiteToMoveOnTrack" ("ThrowValue"))
                                if:(not ("IsFriendAt" (to)))
                                (apply "CaptureMove")
                            )
                            (then
                                (if (not ("ActivatedPiece" (last To)))
                                    ("ActivePiece" (last To))
                                )
                            )
                        )
                    )
                    (move
                        (from 
                            (from)
                            if:(if ("ActivatedPiece" (from))
                                True
                                ("ThrowToActivate")
                            )	
                        )
                        (to 
                            ("SiteToMoveOnTrack" (/ ("ThrowValue") 2))
                            if:(not ("IsFriendAt" (to)))
                            (apply "CaptureMove")
                        )
                        (then
                            (and
                                (if (not ("ActivatedPiece" (last To)))
                                    ("ActivePiece" (last To))
                                )
                                (if (not ("PlayedHalfThrow"))
                                    (and
                                        (set Var 1)
                                        (moveAgain)
                                    )
                                    (set Var 0)
                                )
                            )
                        )
                    )
                )
            )
        )
        (regions "Home" P1 (sites Bottom))
        (regions "Home" P2 (sites Top))
        (map "Throw" {(pair 0 8) (pair 1 0) (pair 2 0) (pair 3 2) (pair 4 12)})
        (dice d:2 from:0 num:4)
    })
    (rules 
        (start { 
            (place "Stick1" (sites Bottom))
            (place "Stick2" (sites Top))
        })
        (play 
            (do 
                (if (not ("PlayedHalfThrow")) (roll))
                next:(if (can Move (forEach Piece))
                    (forEach Piece)
                    (move Pass (then (set Var 0)))
                )
                (then
                    (if ("IsSpecialThrow")
                        (moveAgain)
                    )
                )
            )
        )
        (end 
            (if (and (= 0 ("NumPieceNotInEnemyHome" P1 P2)) (= 0 ("NumPieceNotInEnemyHome" P2 P1)))
                (byScore {
                    (score P1 ("PiecesOwnedOnEnemyHomedBy" P2 P1)) 
                    (score P2 ("PiecesOwnedOnEnemyHomedBy" P1 P2))
                })
            )
        )
    )
)

