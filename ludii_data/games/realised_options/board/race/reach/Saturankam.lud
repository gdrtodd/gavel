(define "ThrowValue" (count Pips))
(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #2 #1))

(define "Move"
    (or
        (if (and {
                (is Occupied (handSite Mover))
                (all DiceEqual) 
                (= 2 (value Player Mover))
            })
            (move
                (from (handSite Mover))
                (to (mapEntry "Entry" Mover))
                (then
                    (and
                        (moveAgain)
                        (set Var "Double" 1)
                    )
                )
            )
        )
        (forEach Piece)
    )		
)

(define "AllReachedCenter" 
    (= 2 (count Pieces Mover in:(sites Centre)))
)

(game "Saturankam"
    (players 4)
    (equipment {
        (board (square 9)
            {
            
            (track "Track1" "81,4,E,N,W,S,E1,N7,E6,S6,W5,N5,E4,S4,W3,N3,E2,S2,W1,N1" P1 directed:True)
            (track "Track2" "83,36,S,E,N,W,S1,E7,S6,W6,N5,E5,S4,W4,N3,E3,S2,W2,N1,E1" P2 directed:True)
            (track "Track3" "83,76,W,S,E,N,W1,S7,W6,N6,E5,S5,W4,N4,E3,S3,W2,N2,E1,S1" P3 directed:True)
            (track "Track4" "84,44,N,W,S,E,N1,W7,N6,E6,S5,W5,N4,E4,S3,W3,N2,E2,S1,W1" P4 directed:True)
            
            }
        )
        (dice d:4 faces:{1 3 4 6} num:2)
        (piece "Marker" Each
            (if
                (and
                    (or { 
                        (= ("SiteToMoveOnTrack" from:(from) 1) (centrePoint))
                        (= ("SiteToMoveOnTrack" from:(from) 3) (centrePoint))
                        (= ("SiteToMoveOnTrack" from:(from) 4) (centrePoint))
                    })
                    (= (value Player Mover) ("ThrowValue"))
                )
                (if 
                    (and 
                        (all DiceEqual)
                        (= ("SiteToMoveOnTrack" from:(from) (/ ("ThrowValue") 2)) (centrePoint))
                    )
                    (move
                        (from (from))
                        (to (centrePoint))
                        (then
                            (and
                                (moveAgain)
                                (set Var "Double" 1)
                            )	
                        )
                    )
                )
                (forEach Value min:1 max:(value Player Mover)
                    (move 
                        (from (from) level:(level))
                        (to
                            ("SiteToMoveOnTrack" from:(from) (value))
                            if:True
                            (apply 
                                (and
                                    (if (and (not (is In (to) (sites "SafeSites"))) ("IsEnemyAt" (to)))
                                        (forEach Level (to) FromTop
                                            (fromTo
                                                (from (to) level:(level))
                                                (to (handSite (who at:(to) level:(level))))
                                            )
                                        )
                                    )
                                    (if (< 0 (- (value Player Mover) (value)))
                                        (and
                                            (moveAgain)
                                            (set Value Mover (- (value Player Mover) (value)))
                                        )
                                        (and
                                            (set Value Mover 0)
                                            (if (all DiceEqual)
                                                (and
                                                    (moveAgain)
                                                    (set Var "Double" 1)
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (hand Each)
        (regions "SafeSites" (sites {4 44 76 36 40}))
        (map "Entry" {(pair P1 4) (pair P2 36) (pair P3 76) (pair P4 44)})
    })
    (rules 
        
        (start {
            (place Stack "Marker1" 4 count:2)
            (place Stack "Marker2" 36 count:2)
            (place Stack "Marker3" 76 count:2)
            (place Stack "Marker4" 44 count:2)
        })
        
        (play 
            (if (or (= 1 (var "Double")) ("NewTurn"))
                ("RollMove" 
                    (move Pass 
                        (then 
                            (and {
                                (set Value Mover ("ThrowValue"))
                                (if (= 1 (var "Double")) (set Var "Double" 0))
                                (moveAgain)
                            })
                        )
                    )
                )
                (if (can Move ("Move")) 
                    ("Move")
                    (move Pass (then (and (set Var "Double" 0) (set Value Mover 0))))
                )
            )
        )
        (end (if ("AllReachedCenter") (result Mover Win)))
    )
)

