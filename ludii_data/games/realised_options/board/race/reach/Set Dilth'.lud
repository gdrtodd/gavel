(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #1))

(define "NextSite" ("NextSiteOnTrack" 1 from:#1 #2))

(define "MadeACompleteCircuit" 
    (is In (mapEntry "Start" Mover) (sites Track Mover "Track" from:("NextSite" (last From) "Track") to:(last To)))
)

(define "Move" 
    (move
        (from (from) level:(level))
        (to 
            ("SiteToMoveOnTrack" ("ThrowValue"))
            if:True
            ("HittingCapture" (mapEntry "Start" (who at:(to))))
        )
        (then
            (forEach Site (sites Track Mover "Track" from:("NextSite" (last From) "Track") to:(last To))
                (if ("IsEnemyAt" (site))
                    (forEach Level (site)
                        (move
                            (from (site) level:(level))
                            (to (mapEntry "Start" (who at:(site) level:(level))))
                        )
                    )
                )
            )
        )
    )
)

(game "Set Dilth'"
    (players 4)
    (equipment {
        ("FortyStonesWithFourGapsBoard"
            
            {
            (track "Track1" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P1)
            (track "Track2" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True P2)
            (track "Track3" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P3)
            (track "Track4" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True P4)
            }
            
        )
        (regions "AllSites" (sites Board))
        (map "Throw" {(pair 0 10) (pair 1 2) (pair 2 3) (pair 3 5)})
        (map "Start" {(pair 1 37) (pair 2 38) (pair 3 39) (pair 4 36)})
        (piece "Stick" Each ("Move"))
        ("StickDice" 3)
    })
    (rules 
        
        (start {
            (set Team 1 {P1 P3})
            (set Team 2 {P2 P4})
            (place Stack "Stick1" 37)
            (place Stack "Stick2" 38)
            (place Stack "Stick3" 39)
            (place Stack "Stick4" 36)
        })
        
        (play 
            ("RollMove" 
                (forEach Piece)
                (then 
                    (do
                        (if ("MadeACompleteCircuit") 
                            (addScore Mover 1) 
                            (then 
                                (if (= (score Mover) 3) 
                                    (remove (last To))
                                )
                            )
                        )
                        next:(if (and (!= 3 (score Mover)) (= 10 ("ThrowValue"))) (moveAgain))
                    )
                )
            )
        )
        (end (if (>= (score Mover) 4) (result Mover Win)))
    )
)

