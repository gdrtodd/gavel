(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "Move" 
    (move
        (from (sites Occupied by:#1))
        (to 
            ("NextSiteOnTrack" ("ThrowValue") #2)
            if:(not ("IsEnemyAt" (to)))
        )
    )
)

(game "Setichch"
    (players 6)
    (equipment {
        ("FortyStonesWithFourGapsBoard"
            {
            (track "TrackCW" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True)
            (track "TrackCCW" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True)
            }
        )
        (regions "AllSites" (sites Board))
        (piece "Stick" P1)
        (piece "Stick" P2)
        ("StickDice" 3)
        (hand Each)
        (map "Throw" {(pair 0 5) (pair 1 3) (pair 2 2) (pair 3 10)})
    })
    (rules 
        (start {
            (place Stack "Stick1" Edge 36 count:2)
            (place Stack "Stick2" Edge 38 count:2)
            
            (set Team 1 {P1 P3 P5})
            (set Team 2 {P2 P4 P6})
            
        })
        (play 
            ("RollMove"
                (if (is In (mover) (players Team1))
                    ("Move" Team1 "TrackCW")
                    ("Move" Team2 "TrackCCW")
                )
                (then (if (= 10 ("ThrowValue")) (moveAgain)))
            )
        )
        (end {
            (if 
                (and {
                    (is In (mover) (players Team1)) 
                    (= 1 (count Sites in:(sites Occupied by:Team1))) 
                    (= 36 (where "Stick" Team1))
                })
                (result Team1 Win)
            )
            (if 
                (and {
                    (is In (mover) (players Team2)) 
                    (= 1 (count Sites in:(sites Occupied by:Team2))) 
                    (= 38 (where "Stick" Team2))
                })
                (result Team2 Win)
            )
        })
    )
)

