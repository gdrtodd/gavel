(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "Throw1" (= ("ThrowValue") 1))
(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #2 #1))
(define "BankorOnTrack" ("NextSiteOnTrack" #2 from:("WhereBankor") #1))
(define "BankorPhaseDone" (or 
        (= ("WhereBankor") ("Center"))
        (and
            (is Occupied ("Center"))
            (>= 1 (count Sites in:(difference (sites Occupied by:All) ("WhereBankor"))))
        )
    )
)

(define "WhereBankor" (where "Bankor" Neutral))
(define "LevelBankor" (where Level "Bankor" Neutral at:("WhereBankor")))
(define "StateBankor" (state at:("WhereBankor") level:("LevelBankor")))

(define "Center" 0)

(define "MoveBankor"
    (move
        (from ("WhereBankor") level:("LevelBankor"))
        (to ("BankorOnTrack" "Track" ("ThrowValue")))
        (then
            (and
                (if (!= (mover) "StateBankor")
                    (set State at:("WhereBankor") level:("LevelBankor") (mover))
                )
                (if (= (last To) ("Center"))
                    (forEach Site (sites Track "Track" from:(last From) to:(last To))
                        (if (!= (site) (last To))
                            (forEach Level (site) FromTop
                                (remove (site) level:(level)) 
                            )
                        )
                    )
                    (forEach Site (sites Track "Track" from:(last From) to:(last To))
                        (if (!= (site) (last To))
                            (forEach Level (site) FromTop
                                (fromTo 
                                    (from (site) level:(level))
                                    (to (last To))
                                )
                            )
                        )
                    )
                )
            )
        )
    )
)

(game "Sik"
    (players 4)
    (equipment {
        (board 
            (spiral turns:5 sites:86) 
            {
            (track "Track" {85..0} directed:True)
            }
            use:Vertex
        )
        ("StickDice" 4)
        (map "Throw" {(pair 0 4) (pair 1 3) (pair 2 0) (pair 3 1) (pair 4 6)})
        (piece "Stick" Each
            (move
                (from (from) level:(level)
                    if:(or 
                        (= 1 (state at:(from) level:(level)))
                        ("Throw1")
                    )
                )
                (to
                    ("SiteToMoveOnTrack" "Track" ("ThrowValue"))
                    if:True
                )
                (then
                    (if (!= 1 (state at:(last To) level:(last LevelTo)))
                        (set State at:(last To) level:(last LevelTo) 1)
                    )
                )
            )
        )
        (piece "Bankor" Neutral)
    })
    (rules 
        (start {
            (place Stack items:{"Stick4" "Stick3" "Stick2" "Stick1"} 85)
            (place Stack "Bankor0" 85)
        })
        
        (play 
            ("RollMove"
                (if (= ("Center") (where "Stick" Mover))
                    (if (< (value Player Mover) 2)
                        (move Pass (then (set Value Mover (+ 1 (value Player Mover)))))
                        (if (or (= (mover) ("StateBankor")) (= 0 ("StateBankor")))
                            (if (!= 0 ("ThrowValue")) ("MoveBankor"))
                        )
                    )
                    (if (!= 0 ("ThrowValue")) (forEach Piece))
                )
            )
        )
        (end {
            (forEach Player
                if:(no Pieces Player) 
                (result Player Loss)
            )
            (forEach Player
                if:("BankorPhaseDone")
                (result Player Win)
            )
        })
    )
)

