(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "TrackMove" ("NextSiteOnTrack" ("ThrowValue") #1))

(define "Move" 
    (move
        (from (from) level:(level))
        (to 
            ("NextSiteOnTrack" ("ThrowValue") #1)
            if:(not ("IsFriendAt" (to)))
            ("HittingCapture" (mapEntry "Start" (who at:(to))))
        )
    )
)

(define "NextSite" ("NextSiteOnTrack" 1 from:#1 #2))

(define "MadeACompleteCircuit" 
    (if (or (is Mover P1) (is Mover P2))
        (is In (mapEntry "Start" Mover) (sites Track "TrackCCW" from:("NextSite" (last From) "TrackCCW") to:(last To)))
        (is In (mapEntry "Start" Mover) (sites Track "TrackCW" from:("NextSite" (last From) "TrackCW") to:(last To)))
    )
)

(game "Tasholiwe"
    (players 4)
    (equipment {
        ("FortyStonesWithFourGapsBoard"
            {
            (track "TrackCW" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True)
            (track "TrackCCW" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True)
            }
        )
        (map "Throw" {(pair 0 10) (pair 1 3) (pair 2 2) (pair 3 5)})
        (map "Start" {(pair 1 39) (pair 2 36) (pair 3 37) (pair 4 38)})
        (regions "AllSites" (sites Board Vertex))
        (piece "Stick" Each)
        ("StickDice" 3)
        (hand Each)
    })
    (rules 
        (start {
            
            (place "Stick1" Edge (mapEntry "Start" P1))
            (place "Stick2" Edge (mapEntry "Start" P2))
            (place "Stick3" Edge (mapEntry "Start" P3))
            (place "Stick4" Edge (mapEntry "Start" P4))
            
        })
        (play 
            ("RollMove" 
                (if (or (is Mover P1) (is Mover P2))
                    (forEach Piece ("Move" "TrackCCW"))
                    (forEach Piece ("Move" "TrackCW"))
                )
                (then 
                    (do
                        (if ("MadeACompleteCircuit") 
                            (addScore Mover 1) 
                            (then 
                                (if (= (score Mover) 4) 
                                    (remove (last To))
                                )
                            )
                        )
                        next:(if (and (!= 4 (score Mover)) (= 10 ("ThrowValue"))) (moveAgain))
                    )
                )
            )
        )
        (end (if (>= (score Mover) 4) (result Mover Win)))
    )
)

