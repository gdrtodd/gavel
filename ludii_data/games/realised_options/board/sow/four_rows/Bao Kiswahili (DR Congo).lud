(define "Columns" 8)
(define "NoPiecesInInner" (all Sites (sites #1 "Inner") if:(= 0 (count at:(site)))))
(define "Sowing"
    (sow
        #1
        owner:(mover)
        apply:(if (is In (to) (sites Mover "Inner"))
            (if (is Occupied ("OppositePit" (to)))
                (and {
                    (moveAgain)
                    (set Pending)
                    (if (is In (to) (expand (sites Right)))
                        (set Value Mover 2) 
                        (if (is In (to) (expand (sites Left)))
                            (set Value Mover 1) 
                            (set Value Mover 0) 
                        )
                    )
                })
                (if (< 1 (count at:(to)))
                    (and {
                        (moveAgain)
                        (set Var "Replay" (to))
                        (set Var "Direction" #2) 
                    })
                )
            )
            (if (< 1 (count at:(to)))
                (and {
                    (moveAgain)
                    (set Var "Replay" (to))
                    (set Var "Direction" #2) 
                })
            )
        )
    )
)

(define "SowingPhase2"
    (move Select
        (from 
            (sites Mover)
            if:(is Occupied (from))
        )
        (then
            (sow
                #1
                owner:(mover)
                apply:(if (< 1 (count at:(to)))
                    (and {
                        (moveAgain)
                        (set Var "Replay" (to))
                        (set Var "Direction" #2) 
                    })
                )
            )
        )
    )
)

(game "Bao Kiswahili (DR Congo)" 
    (players 2) 
    (equipment { 
        (mancalaBoard 4 8 store:None
            {
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            (track "TrackCW1" "7,W,N1,E" loop:True P1)
            (track "TrackCW2" "23,W,N1,E" loop:True P2)
            }
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions "Home" P1 (expand (sites Bottom)))   
        (regions "Home" P2 (expand (sites Top)))    
        (regions "Inner" P1 (sites Row 1))      
        (regions "Inner" P2 (sites Row 2))      
        (map "LeftMostInner" {(pair P1 8) (pair P2 23)})
        (map "RightMostInner" {(pair P1 15) (pair P2 16)})
        (map "AdjacentLeftMostInner" {(pair P1 9) (pair P2 22)})
        (map "AdjacentRightMostInner" {(pair P1 14) (pair P2 17)})
    }) 
    (rules 
        (start {
            (set Count 24 at:(handSite P1))
            (set Count 24 at:(handSite P2))
            (set Count 1 to:(union (sites Row 1) (sites Row 2))) 
        })
        
        phases:{
        (phase "Placement"
            (play 
                (if ("SameTurn")
                    (if (is Pending) 
                        (or
                            (if (or (= 1 (value Player Mover)) (= 0 (value Player Mover))) 
                                (do 
                                    (fromTo
                                        (from ("OppositePit" (last To afterConsequence:True)))
                                        (to (mapEntry "LeftMostInner" Mover))
                                        count:(count at:("OppositePit" (last To afterConsequence:True)))
                                    )
                                    next:(move Select
                                        (from (mapEntry "LeftMostInner" Mover))
                                        (then ("Sowing" "TrackCW" 1))
                                    )
                                )
                            )
                            (if (or (= 2 (value Player Mover)) (= 0 (value Player Mover))) 
                                (do 
                                    (fromTo
                                        (from ("OppositePit" (last To afterConsequence:True)))
                                        (to (mapEntry "RightMostInner" Mover))
                                        count:(count at:("OppositePit" (last To afterConsequence:True)))
                                    )
                                    next:(move Select
                                        (from (mapEntry "RightMostInner" Mover))
                                        (then ("Sowing" "TrackCCW" 2))
                                    )
                                )
                            )
                        )
                        
                        (or
                            (if (or (= (var "Direction") 1) (= (var "Direction") 3))
                                (move Select
                                    (from (var "Replay"))
                                    (then ("Sowing" "TrackCW" 1))
                                )
                            )
                            (if (or (= (var "Direction") 2) (= (var "Direction") 3))
                                (move Select
                                    (from (var "Replay"))
                                    (then ("Sowing" "TrackCCW" 2))
                                )
                            )
                        )
                    )
                    (if (is Occupied (handSite Mover)) 
                        (priority {
                            (move 
                                (from (handSite Mover))
                                (to (sites Mover "Inner") if:(is Occupied ("OppositePit" (to))))
                                count:1
                                (then
                                    (and {
                                        (moveAgain)
                                        (set Pending)
                                        (if (is In (last To) (expand (sites Right)))
                                            (set Value Mover 2) 
                                            (if (is In (last To) (expand (sites Left)))
                                                (set Value Mover 1) 
                                                (set Value Mover 0) 
                                            )
                                        )
                                    })
                                )
                            )
                            (move 
                                (from (handSite Mover))
                                (to (sites Mover "Inner"))
                                count:1
                                (then
                                    (and {
                                        (moveAgain)
                                        (set Var "Replay" (last To))
                                        (set Var "Direction" 3) 
                                    })
                                )
                            )
                        })
                        
                        (or 
                            ("SowingPhase2" "TrackCW" 1)
                            ("SowingPhase2" "TrackCCW" 2)
                        )
                    )
                )
            )
        )
        }
        
        (end 
            (forEach Player
                if:("NoPiecesInInner" Player)
                (result Player Loss)
            )
        )
    )
)

