(define "Columns" 16)
(define "NextHoleFrom" ("NextSiteOnTrack" #3 from:#1 #2))
(define "LeftMost" 
    (trackSite FirstSite 
        from:(trackSite FirstSite if:(not (is Mover (who at:(to)))))
        if:(is Mover (who at:(to)))
    )
)

(game "Chiana wa Kunja"
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            (track "Track" "0,E,63,W" loop:True)
        )
        (piece "Seed" Each)
        (hand Shared)
    }) 
    
    (rules 
        (start {
            (place "Seed1" (sites Bottom) counts:{4})
            (place "Seed2" (sites Top) counts:{4})
        })
        
        (play 
            (or
                (move Select 
                    (from ("LeftMost"))
                    (then
                        (do
                            (and
                                (forEach Site
                                    (sites Track "Track" from:(last From) to:(trackSite Move from:(last From) steps:(count at:(last From))))
                                    (if (!= (mover) (who at:(site)))
                                        (and 
                                            (remove (site) count:(count at:(site)))
                                            (add (piece (id "Seed" Mover)) (to Cell (handSite Shared)) count:(count at:(site)))
                                        )
                                    )
                                )
                                (set Var "NumSowed" (count at:(last From)))
                            )
                            next:(sow)
                            (then
                                (and {
                                    (if (< 1 (var "NumSowed"))
                                        (if (and
                                                (= 1 (count at:("NextHoleFrom" (last From) "Track" (var "NumSowed"))))
                                                (= 2 (count at:("NextHoleFrom" (last From) "Track" (- (var "NumSowed") 1))))
                                            )
                                            (moveAgain)
                                        )
                                    )
                                    (if (!= 0 (what Cell at:(handSite Shared)))
                                        (fromTo
                                            (from Cell (handSite Shared))
                                            (to (last From))
                                            count:(count Cell at:(handSite Shared))
                                        )
                                    )
                                    (set Var "NumSowed" 0)
                                })
                            )
                        )
                    )
                )
                (if (and (= 1 (count Pieces Mover)) (is Empty ("NextHoleFrom" (from) "Track" 1)))
                    (move
                        (from ("LeftMost"))
                        (to 
                            ("NextHoleFrom" (from) "Track" 2)
                            (apply
                                (and
                                    (remove (to) count:(count at:(to)))
                                    (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                )	
                            )	
                        )
                    )
                )
            )		
        )
        
        (end ("BlockWin"))
    )
)

