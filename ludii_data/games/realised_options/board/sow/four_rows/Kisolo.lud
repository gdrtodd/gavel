(define "Columns" 7)

(define "EmptyOuterRow" (all Sites (sites #1 "Outer") if:(= 0 (count at:(site)))))

(define "InnerPit" (if (is Mover P1) (- #1 "Columns") (+ #1 "Columns")))

(define "InnerPitsWithPossibleCapture"
    (forEach (difference (sites Mover "Inner") (sites Empty))
        if:(is Occupied ("OppositePit" (site)))
    )		
)

(define "NextHole" ("NextSiteOnTrack" 1 from:#1 "TrackCCW"))

(define "LastHole" (last To afterConsequence:True))

(game "Kisolo" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "14,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))          
        (regions "Home" P2 (sites Track "TrackCCW2"))          
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom)))  
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top)))   
        (regions "Outer" P1 (difference (sites Track "TrackCCW1") (sites P1 "Inner"))) 
        (regions "Outer" P2 (difference (sites Track "TrackCCW2") (sites P2 "Inner"))) 
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start {
            (set Count 3 to:(union (sites P1 "Outer") (sites P2 "Outer")))
        })
        
        (play
            (if (and (or (= 0 (count MovesThisTurn)) (is In 1 (sites Pending))) (!= 0 (count Sites in:(("InnerPitsWithPossibleCapture")))))
                (move Select
                    (from ("InnerPitsWithPossibleCapture"))
                    (then 
                        (and {
                            (fromTo 
                                (from ("OppositePit" (last To)))
                                (to ("InnerPit" (last To)))
                            )
                            (if (is Occupied ("OppositeOuterPit" (last To)))
                                (fromTo 
                                    (from ("OppositeOuterPit" (last To)))
                                    (to ("InnerPit" (last To)))
                                )
                            )
                            (moveAgain)
                            }
                            (then
                                (if (!= 0 (count Sites in:(("InnerPitsWithPossibleCapture"))))
                                    (set Pending 1)
                                )
                            )
                        )
                    )
                )
                (move Select
                    (from 
                        (if (and (is In 2 (sites Pending)) ("SameTurn")) 
                            (sites {("NextHole" "LastHole")})
                            (sites Mover "Home") 
                        )
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (is Occupied ("NextHole" (to)))
                                (and { 
                                    (if (and (is In ("NextHole" (to)) (sites Mover "Inner")) (is Occupied ("OppositePit" ("NextHole" (to)))))
                                        (and
                                            (fromTo 
                                                (from ("OppositePit" ("NextHole" (to))))
                                                (to ("InnerPit" ("NextHole" (to))))
                                            )
                                            (if (is Occupied ("OppositeOuterPit" ("NextHole" (to))))
                                                (fromTo 
                                                    (from ("OppositeOuterPit" ("NextHole" (to))))
                                                    (to ("InnerPit" ("NextHole" (to))))
                                                )
                                            )		
                                        )
                                    )
                                    (moveAgain)
                                    (set Pending 2)
                                })
                            )
                        )
                    )
                )
            )
        )
        
        (end 
            (forEach Player
                if:("EmptyOuterRow" Player)
                (result Player Loss)
            )
        )
    )
)

