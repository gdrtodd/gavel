(define "Columns" 6)

(define "PlayFrom" 
    (play 
        (move Select 
            (from 
                (if ("SameTurn") 
                    "LastHoleSowed" 
                    (sites Mover #1) 
                ) 
                if:(> (count at:(from)) 0)
            )
            (then 
                (sow
                    "Track"
                    owner:(mover)
                    apply:(if (= (count at:(to)) 1)
                        (if (is In (to) (sites Mover "Inner"))
                            (if (> (count at:("OppositePit" (to))) 0) 
                                (and
                                    (fromTo 
                                        (from ("OppositePit" (to))) 
                                        (to (handSite Mover)) 
                                        count:(count at:("OppositePit" (to)))
                                    ) 
                                    (if 
                                        (> (count at:("OppositeOuterPit" (to))) 0) 
                                        (fromTo 
                                            (from ("OppositeOuterPit" (to))) 
                                            (to (handSite Mover)) 
                                            count:(count at:("OppositeOuterPit" (to)))
                                        ) 
                                    ) 
                                ) 
                            ) 
                        )
                        (moveAgain)
                    )
                )
            )
        )
    )
)

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

(game "Muvalavala (Luena)" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "5,W,N1,E" loop:True P1)
            (track "Track2" "17,W,N1,E" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))        
        (regions "Home" P2 (sites Track "Track2"))        
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) 
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top))) 
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home"))))
        
        phases:{
        (phase "Opening" 			 	
            ("PlayFrom" "Inner")
            (nextPhase (>= (count Turns) 2) "Playing")
        )
        (phase "Playing" 
            ("PlayFrom" "Home")
        )
        }
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

