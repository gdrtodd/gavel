(define "Columns" 6)

(define "PiecesOwnedBy" (count in:(sites #1 "Home")))

(define "FirstSiteToSow"
    (if (is Mover P1)
        (intersection (sites Right) (sites Row 1))
        (intersection (sites Left) (sites Row 2))
    )	
)

(define "NextSite" ("NextSiteOnTrack" 1 #1))

(game "Nchuwa" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "12,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))      
        (regions "Home" P2 (sites Track "TrackCCW2"))      
        (regions "Inner" P1 (sites Row 1))         
        (regions "Inner" P2 (sites Row 2))         
        (regions "Outer" P1 (sites Row 0))         
        (regions "Outer" P2 (sites Row 3))         
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start 
            (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home")))
        )
        
        phases:{
        (phase "Opening1"
            (play
                (move Select
                    (from ("FirstSiteToSow"))
                    (then 
                        (sow 
                            "TrackCCW"
                            owner:(mover)
                            apply:(set Value Mover ("NextSite" from:(to)))
                        )
                    )
                )
            )
            (nextPhase Mover "Opening2")
        )
        (phase "Opening2"
            (play
                (move Select
                    (from (value Player Mover))
                    (then 
                        (sow 
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (= (count at:("NextSite" from:(to))) 0) 
                                (set Value Mover Off)
                                (set Value Mover ("NextSite" from:(to)))
                            )
                        )
                    )
                )
            )
            (nextPhase Mover ("IsOffBoard" (value Player Mover)) "Opening3")
        )
        (phase "Opening3"
            (play
                (move
                    (from (forEach (sites Mover "Inner") if:(!= 0 (count at:(site)))))
                    (to (forEach (sites Mover "Outer") if:(= 0 (count at:(site)))))
                    count:2
                )
            )
            (nextPhase Mover "Opening4")
        )
        (phase "Opening4"
            (play (move Remove (sites Next "Home")))
            (nextPhase Mover "Opening5")
        )
        (phase "Opening5"
            (play (move Remove (sites Next "Home")))
            (nextPhase Mover "Playing")
        )
        (phase "Playing"
            (play
                (move Select 
                    (from 
                        (if ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover "Home") 
                        ) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (= (count at:(to)) 1)
                                (if (is In (to) (sites Mover "Inner"))
                                    (if (> (count at:("OppositePit" (to))) 0) 
                                        (and
                                            (remove ("OppositePit" (to))) 
                                            (if 
                                                (> (count at:("OppositeOuterPit" (to))) 0) 
                                                (remove ("OppositeOuterPit" (to))) 
                                            ) 
                                        ) 
                                    ) 
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end 
            (forEach Player
                if:(= 0 ("PiecesOwnedBy" Player))
                (result Player Loss)
            )
        )
    )
)

