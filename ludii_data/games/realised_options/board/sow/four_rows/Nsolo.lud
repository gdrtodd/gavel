(define "Columns" 10)
(define "NoPiece" (all Sites (sites Player "Home") if:(= 0 (count at:(site)))))
(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))
(define "AllSitesNoMoreThanOne"
    (all Sites (sites Mover "Home") if:(>= 1 (count at:(site))))
)

(game "Nsolo" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" "20,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))        
        (regions "Home" P2 (sites Track "Track2"))        
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) 
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top))) 
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start 
            (set Count 2 to:(union {
                    (sites Bottom)
                    (sites Top)
                    (difference (sites P1 "Inner") (sites Right))
                    (difference (sites P2 "Inner") (sites Left))
                })
            )
        )
        
        (play 
            (if (and ("SameTurn") (= (var "Replay") Undefined))
                (move Remove 
                    (sites Next "Home")
                )
                (if ("AllSitesNoMoreThanOne")
                    (move Select
                        (from (sites Mover "Home") if:(and (is Occupied (from)) (is Empty ("NextHole" (from) 1))))
                        (then 
                            (sow
                                "Track"
                                owner:(mover)		
                            )
                        )
                    )
                    (move Select
                        (from 
                            (if 
                                ("SameTurn") 
                                (sites {(var "Replay") })
                                (sites Mover "Home") 
                            ) 
                            if:(> (count at:(from)) 1)
                        )
                        (then
                            (sow
                                "Track"
                                owner:(mover)
                                apply:(if (< 1 (count at:(to)))
                                    (if (and 
                                            (is In (to) (sites Mover "Inner"))
                                            (is Occupied ("OppositePit" (to)))
                                        )
                                        (and
                                            (remove ("OppositePit" (to)))
                                            (if (is Occupied ("OppositeOuterPit" (to)))
                                                (and {
                                                    (moveAgain)
                                                    (set Var "Replay" Undefined)
                                                    (remove ("OppositeOuterPit" (to)))
                                                })	
                                            )
                                        )
                                        (and
                                            (moveAgain)
                                            (set Var "Replay" (to))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        
        (end
            (forEach NonMover
                if:("NoPiece")
                (result Player Loss)
            )
        )
    )
)

