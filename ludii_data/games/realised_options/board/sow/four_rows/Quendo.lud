(define "Columns" 8)
(define "NumSeed" 4)
(define "LeftMost" 
    (trackSite FirstSite 
        from:(trackSite FirstSite if:(not (is Mover (who at:(to)))))
        if:(is Mover (who at:(to)))
    )
)

(game "Quendo" 
    (players 4) 
    
    (equipment { 
        
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track" "0,E,N,W,S2" loop:True)
            }
        )
        
        (piece "Seed" Each)
        (hand Each)
    }) 
    
    (rules 
        
        (start {
            (place "Seed1" (sites {0..4}) counts:{"NumSeed"})
            (place "Seed2" (sites {5..7 15 23}) counts:{"NumSeed"})
            (place "Seed3" (sites {31..27}) counts:{"NumSeed"})
            (place "Seed4" (sites {26..24 16 8}) counts:{"NumSeed"})
        })
        
        (play 
            (if (!= 1 (count Pieces Mover))
                (move
                    Select 
                    (from 
                        ("LeftMost") 
                    )
                    (then
                        (sow
                            sowEffect:(if (and (!= (mover) (what at:(to))) (is Occupied (to)))
                                (and
                                    (remove (to) count:(count at:(to)))
                                    (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                )
                            )		
                        )
                    )
                )
                (move
                    (from ("LeftMost"))
                    (to 
                        (trackSite FirstSite from:(from) if:("IsEnemyAt" (to)))
                        (apply
                            (and
                                (remove (to) count:(count at:(to)))
                                (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                            )	
                        )	
                    )
                )
            )
        )
        
        (end 
            (forEach NonMover 
                if:(no Pieces Player) 
                (result Player Loss)
            )
        )
    )
)

