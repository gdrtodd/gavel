(define "Rows" 4)
(define "OppositePit" (if (is Mover P1) (+ (to) "Rows") (- (to) "Rows")))
(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Rows" 2)) (- (to) (* "Rows" 2))))
(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1 "Home"))))
(define "NextSite" (trackSite Move from:#1 #2 steps:#3))

(game "Tchela"
    (players 2)
    (equipment {
        (board 
            
            (merge {
                (square 4)
                (shift 5 0 (square 4))
                (shift 4 0.5 (square 1))
                (shift 4 2.5 (square 1))
                (shift -1 2.5 (square 1))
                (shift 9 0.5 (square 1))
            })
            
            { 
            (track "TrackCCW1" {0..3 16..19 23..20 7..4} loop:True P1)
            (track "TrackCCW2" {8..11 24..27 31..28 15..12} loop:True P2)
            (track "TrackExtendedCCW1" {0..3 16..19 23..20 32 7..4} loop:True P1)
            (track "TrackExtendedCCW2" {8..11 33 24..27 31..28 15..12} loop:True P2)
            }
            
            use:Vertex
        )
        (piece "Seed" Shared)
        (regions "HomeNotExtented" P1 (sites Track "TrackCCW1")) 
        (regions "HomeNotExtented" P2 (sites Track "TrackCCW2")) 
        (regions "Home" P1 (sites Track "TrackExtendedCCW1")) 
        (regions "Home" P2 (sites Track "TrackExtendedCCW2")) 
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom))) 
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top))) 
        
        (map {(pair P1 34) (pair P2 35)}) 
        (map "ExtraStore" {(pair P1 32) (pair P2 33)})
        
    })
    (rules
        
        (start 
            (set Count 1 
                to:(difference 
                    (sites Board)
                    (sites {32..35})
                )
            )
        )
        
        phases:{
        (phase "Opening"
            (play
                (move
                    Select
                    (from
                        (if (< (value Player Mover) 0)
                            (sites Mover "HomeNotExtented")
                            (sites {("NextSite" (value Player Mover) "TrackCCW" 2)})
                        )
                    )
                    (then 
                        (and
                            (sow
                                "TrackCCW"
                                owner:(mover)
                            )
                            (set Value Mover (last To))
                        )
                    )
                )
            )
            (nextPhase Mover (= 0 (count at:("NextSite" (value Player Mover) "TrackCCW" 2))) "OpeningPair")
        )
        
        (phase "OpeningPair"
            (play
                (move Select
                    (from (sites {("NextSite" (value Player Mover) "TrackCCW" 1)}))
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            
            (nextPhase Mover "Playing")
        )
        
        (phase "Playing"
            (play
                (or
                    (move Select
                        (from 
                            (if 
                                ("SameTurn") 
                                "LastHoleSowed" 
                                (sites Mover "HomeNotExtented") 
                            ) 
                            if:(< 0 (count at:(from)))
                        )
                        (then 
                            (sow
                                "TrackCCW"
                                owner:(mover)
                                apply:(if (> (count at:(to)) 1)
                                    (moveAgain)
                                    (if (and 
                                            (is In (to) (sites Mover "Inner")) 
                                            (> (count at:"OppositePit") 0)
                                        )
                                        (and
                                            (fromTo 
                                                (from "OppositePit") 
                                                (to (mapEntry Mover)) 
                                                count:(count at:"OppositePit")
                                            ) 
                                            (if (> (count at:"OppositePit") 0)
                                                (fromTo 
                                                    (from ("OppositeOuterPit")) 
                                                    (to (mapEntry Mover)) 
                                                    count:(count at:("OppositeOuterPit"))
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (do
                        (move Select
                            (from 
                                (if 
                                    ("SameTurn") 
                                    "LastHoleSowed" 
                                    (sites Mover "Home") 
                                ) 
                                if:(< 0 (count at:(from)))
                            )
                            (then 
                                (sow
                                    "TrackExtendedCCW"
                                    owner:(mover)
                                    apply:(if (!= (to) (mapEntry "ExtraStore" Mover))
                                        (if (> (count at:(to)) 1)
                                            (moveAgain)
                                            (if (and 
                                                    (is In (to) (sites Mover "Inner")) 
                                                    (> (count at:"OppositePit") 0)
                                                )
                                                (and
                                                    (fromTo 
                                                        (from "OppositePit") 
                                                        (to (mapEntry Mover)) 
                                                        count:(count at:"OppositePit")
                                                    ) 
                                                    (if (> (count at:"OppositePit") 0)
                                                        (fromTo 
                                                            (from ("OppositeOuterPit")) 
                                                            (to (mapEntry Mover)) 
                                                            count:(count at:("OppositeOuterPit"))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                        ifAfterwards:(<= (count at:(mapEntry "ExtraStore" Mover)) 1)
                    )
                )
            )
        )
        }
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

