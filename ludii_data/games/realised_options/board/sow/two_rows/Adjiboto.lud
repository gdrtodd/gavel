(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))

(define "RemmemberSowedHoles" 
    (if (is Mover P1)
        (remember Value "SowedHolesP1" #1 unique:True)
        (remember Value "SowedHolesP2" #1 unique:True)
    )
)

(define "SowedHoles" 
    (if (is Mover P1)
        (values Remembered "SowedHolesP1")
        (values Remembered "SowedHolesP2")
    )
)

(game "Adjiboto" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 5 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      
        (regions P2 (sites Top))      
    }) 
    (rules 
        
        (start (set Count 10 to:(sites Track)))
        
        phases:{
        (phase "Opening"
            (play
                (move Select
                    (from (difference (sites Mover) (sites ("SowedHoles"))) if:(< 1 (count at:(to))))
                    (then
                        (and
                            (sow origin:True)
                            ("RemmemberSowedHoles" (last From))
                        )
                    )
                )
            )
            (nextPhase Mover (< 8 (count Turns)))
        )
        (phase "Main"
            (play
                (move Select
                    (from (sites Mover) if:(< 1 (count at:(to))))
                    (then
                        (sow
                            if:(or {
                                (= 1 (count at:("NextHole" (to) 1)))
                                (= 3 (count at:("NextHole" (to) 1)))
                                (= 5 (count at:("NextHole" (to) 1)))
                            })
                            apply:(fromTo
                                (from ("NextHole" (to) 1))
                                (to (handSite Mover))
                                count:(count at:("NextHole" (to) 1))
                            )
                            origin:True		
                            forward:True
                        )
                    )
                )
            )
            (end 
                (if (no Moves Next) 
                    {
                    (if (< (count Cell at:(handSite Next)) 50) (result Mover Win))
                    (if (>= (count Cell at:(handSite Next)) 50) (result Mover Draw))
                    }
                )
                
            )
        )
        }
    )
)

