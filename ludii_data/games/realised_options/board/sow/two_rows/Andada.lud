(define "Columns" 12)

(define "PlayFromLastHole" (last To afterConsequence:True))

(define "NextHole" ("NextSiteOnTrack" 1 from:#1 "Track"))

(define "NoCounters" (all Sites (sites #1) if:(= 0 (count at:(site)))))

(define "OnlyOneCounterInEachHole" 
    (all Sites 
        (sites Board) 
        if:(<= (count at:(site)) 1)
    )
)

(game "Andada" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      
        (regions P2 (sites Top))      
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 2 to:(sites Track)))
        
        phases:{
        (phase "Opening"
            (play 
                (move Select 
                    (from 
                        (if ("SameTurn") 
                            (sites {("NextHole" ("PlayFromLastHole"))})
                            (sites Mover) 
                        )
                        if:(> (count at:(from)) 0)
                    )
                    (then (sow (then (if (is Occupied ("NextHole" ("PlayFromLastHole"))) (moveAgain)))))
                )	
            )
            
            (nextPhase (is Mover P2) "Playing")
        )
        
        (phase "Playing"
            (play 
                (move Select 
                    (from 
                        (if ("SameTurn") 
                            (sites {"PlayFromLastHole"})
                            (sites Mover) 
                        )
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            apply:(if (= 1 (count at:(to)))
                                (if ("OnlyOneCounterInEachHole")
                                    (if (is In ("NextHole" (to)) (sites Next))
                                        (fromTo 
                                            (from (to)) 
                                            (to (handSite Mover)) 
                                            count:(count at:(to))
                                        )
                                    )
                                    (if (and (is Occupied ("OppositePit" (to))) (is In (to) (sites Mover)))
                                        (fromTo 
                                            (from ("OppositePit" (to))) 
                                            (to (handSite Mover)) 
                                            count:(count at:("OppositePit" (to)))
                                        )
                                    )
                                )
                                (moveAgain)
                            )
                        )		
                    )
                )		
            )
            (end
                (forEach Player
                    if:("NoCounters" Player)
                    (result Player Loss)
                )
            )
        )
        }
    )
)

