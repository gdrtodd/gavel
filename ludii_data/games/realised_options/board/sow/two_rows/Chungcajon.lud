(define "PiecesOwnedBy" (count at:(mapEntry #1)))

(define "NotMoverStore" (!= (to) (mapEntry Mover)))

(game "Chungcajon" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 7
            {
            (track "Track1" "7,W,WNW,ENE,E" loop:True P1)
            (track "Track2" "8,E,ESE,WSW,W" loop:True P2)
            }
        )
        (regions P1 (sites Bottom))      
        (regions P2 (sites Top))      
        (map {(pair P1 FirstSite) (pair P2 LastSite)}) 
        (piece "Seed" Shared)
    }) 
    
    (rules 
        (start (set Count 7 to:(union (sites P1) (sites P2))))		 
        (play 
            (move Select 
                (from 
                    (if 
                        ("SameTurn") 
                        "LastHoleSowed" 
                        (sites Mover) 
                    ) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow 
                        "Track"
                        owner:(mover)
                        if:(and 
                            (> (count at:(to)) 1)
                            ("NotMoverStore")
                        ) 
                        apply:(moveAgain)
                    )
                )
            )
        )
        (end ("MancalaByScoreWhen" (all Sites (union (sites P1) (sites P2)) if:(= 0 (count at:(site))))))
    )
)

