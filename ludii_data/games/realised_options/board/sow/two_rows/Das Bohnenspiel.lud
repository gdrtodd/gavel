(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

(game "Das Bohnenspiel" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 6
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions "Home" P1 (sites Bottom)) 			 
        (regions "Home" P2 (sites Top)) 				
        (map {(pair P1 FirstSite) (pair P2 LastSite)}) 
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start (set Count 6 to:(sites Track)))
        
        (play 
            (move Select 
                (from
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        if:(is In (count at:(to)) (sites {2 4 6})) 
                        apply:(fromTo 
                            (from (to)) 
                            (to (mapEntry (mover))) 
                            count:(count at:(to))
                        ) 
                        backtracking:True
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

