(define "Columns" 6)

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

(game "FairKalah" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 "Columns"
            (track "Track" "0,ESE,E,ENE,WNW,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      
        (regions P2 (sites Top))      
        (map {(pair P1 LastSite) (pair P2 FirstSite)}) 
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start {
            
            (set Count 4 at:1)
            (set Count 4 at:2)
            (set Count 5 at:3)
            (set Count 4 at:4)
            (set Count 3 at:5)
            (set Count 4 at:6)
            (set Count 0 at:13)
            (set Count 4 at:12)
            (set Count 4 at:11)
            (set Count 4 at:10)
            (set Count 4 at:9)
            (set Count 4 at:8)
            (set Count 4 at:7)
            (set Count 0 at:0)
            
        })
        
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                ) 
                (then 
                    (sow
                        apply:(if (= (to) (mapEntry (mover)))
                            (moveAgain)
                            (if (and { 
                                    (is In (to) (sites Mover)) 
                                    (= (count at:(to)) 1) 
                                    (>= (count at:("OppositePit" (to))) 0)
                                })
                                (and 
                                    (fromTo 
                                        (from (to)) 
                                        (to (mapEntry (mover))) 
                                        count:(count at:(to))
                                    ) 
                                    (fromTo 
                                        (from ("OppositePit" (to))) 
                                        (to (mapEntry (mover))) 
                                        count:(count at:("OppositePit" (to)))
                                    ) 
                                ) 
                            )
                        )
                        skipIf:(= (to) (mapEntry (next)))
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen"
                (or 
                    (= (count in:(sites Bottom)) 0)
                    (= (count in:(sites Top)) 0)
                )
            )
        )
    )
)

