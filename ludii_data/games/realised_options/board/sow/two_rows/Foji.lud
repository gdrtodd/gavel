(define "Columns" 6)
(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

(game "Foji" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "5,W,N,E" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      
        (regions P2 (sites Top))      
    }) 
    (rules 
        
        (start (set Count 4 to:(sites Track)))
        
        (play 
            (if (is Proposed "End")
                (or (move Vote "End") (move Vote "No"))
                (or
                    (if (is Cycle) (move Propose "End")) 
                    (if ("SameTurn") 
                        (move Select
                            (from "LastHoleSowed")
                            (then
                                (sow
                                    apply:(if (< 1 (count at:(to)))
                                        (moveAgain)
                                        (if (is In (to) (sites Mover))
                                            (if (is Occupied ("OppositePit" (to)))
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to (handSite Mover))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                            )
                                        )
                                    )	
                                    includeSelf:False
                                )
                            )
                        )
                        (do
                            (move Select
                                (from 
                                    (sites Mover) 
                                    if:(is Occupied (from))
                                )
                                (then
                                    (sow
                                        apply:(if (< 1 (count at:(to)))
                                            (moveAgain)
                                            (if (is In (to) (sites Mover))
                                                (if (is Occupied ("OppositePit" (to)))
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (handSite Mover))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                )
                                            )
                                        )	
                                        includeSelf:False
                                    )
                                )
                            )
                            ifAfterwards:(if (is Mover P1) (< 0 (count in:(sites P2))) (< 0 (count in:(sites P1))))
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (or (no Moves Mover) (is Decided "End"))))
    )
)

