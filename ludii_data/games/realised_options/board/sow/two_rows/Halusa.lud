(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

(game "Halusa" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      
        (regions P2 (sites Top))      
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        (start (set Count 6 to:(sites Track)))
        
        (play (do (move Select 
                    (from 
                        (sites Mover) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            if:(and 
                                (is In (to) (sites Mover)) 
                                (is Even (count at:(to)))
                            ) 
                            apply:(fromTo 
                                (from (to)) 
                                (to (handSite Mover)) 
                                count:(count at:(to))
                            )
                            backtracking:True
                        )
                    )
                )
                ifAfterwards:(> (count in:(sites Next)) 0)
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

