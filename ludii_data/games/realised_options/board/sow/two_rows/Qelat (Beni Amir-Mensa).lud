(define "PiecesOwnedBy" (count in:(forEach (sites Board) if:(= (id #1) (state at:(site))))))

(game "Qelat (Beni Amir-Mensa)"
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "5,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (regions "Left" P1 (intersection (expand (sites Left) steps:2) (sites Bottom))) 
        (regions "Left" P2 (intersection (expand (sites Right) steps:2) (sites Top))) 
        (regions "Right" P1 (intersection (expand (sites Right) steps:2) (sites Bottom))) 
        (regions "Right" P2 (intersection (expand (sites Left) steps:2) (sites Top)))  
    }) 
    (rules 
        (start (set Count 4 to:(sites Track)))
        
        (play 
            (or 
                (move Select
                    (from (sites Mover "Left") if:(and (= 0 (state at:(from))) (is Occupied (from))))
                    (then
                        (sow
                            "TrackCW"
                            apply:(if (and {
                                    (is In (to) (union (expand (sites Right)) (expand (sites Left))))
                                    (= 4 (count at:(to)))
                                    (= 0 (state at:(to)))
                                })
                                (set State at:(to) (mover))
                            )
                        )
                    )
                )
                (move Select
                    (from (sites Mover "Right") if:(and (= 0 (state at:(from))) (is Occupied (from))))
                    (then
                        (sow
                            "TrackCCW"
                            apply:(if (and {
                                    (is In (to) (union (expand (sites Right)) (expand (sites Left))))
                                    (= 4 (count at:(to)))
                                    (= 0 (state at:(to)))
                                })
                                (set State at:(to) (mover))
                            )
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (and (no Moves P1) (no Moves P2))))
    )
)

