(define "Columns" 12)

(define "LastHole" (last To afterConsequence:True))

(define "LastHoleSowed" (sites {("LastHole")}))

(define "NoPiece" (= (count in:(sites Player "Home")) 0))

(define "NextHoleFrom" ("NextSiteOnTrack" #2 from:#1))

(game "Tapata"
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "TrackCCW" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Bottom))              
        (regions "Home" P2 (sites Top))              
        (regions "Right Half" P1 (intersection (sites Bottom) (expand (sites Right) steps:5))) 
        (regions "Right Half" P2 (intersection (sites Top) (expand (sites Left) steps:5)))  
        (map {(pair P1 0) (pair P2 23)})              
    }) 
    (rules 
        (start (set Count 3 to:(sites {1 2 4 5 7 8 10 11 22 21 19 18 16 15 13 12})))
        
        phases:{
        
        (phase "FirstMove" 
            (play 
                (move Select 
                    (from (difference (sites Mover "Right Half") (sites Empty)))
                    (then 
                        (sow
                            apply:(if (is In (to) (sites Mover "Home"))
                                (fromTo 
                                    (from (to))
                                    (to (mapEntry Next))
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover "Play")
        )
        
        (phase "Play" 
            (play
                (move Select 
                    (from 
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover "Home") 
                        ) 
                        if:(and
                            (> (count at:(from)) 0)
                            (or ("SameTurn")
                                (and ("NewTurn")
                                    (not 
                                        (and 
                                            (is In ("NextHoleFrom" (from) (count at:(from))) (sites Mover "Home")) 
                                            (!= 0 (count at:("NextHoleFrom" (from) (count at:(from)))))
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (then 
                        (sow
                            apply:(if (< 1 (count at:(to)))
                                (moveAgain)
                                (if (and (!= 0 (count at:("OppositePit" (to)))) (is In (to) (sites Mover "Home")))
                                    (remove ("OppositePit" (to)))
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end
            (forEach NonMover
                if:("NoPiece")
                (result Player Loss)
            )
        )
    )
)

