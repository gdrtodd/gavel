(define "Sow" 
    (move Select 
        (from 
            (sites Mover #1) 
            if:(> (count at:(from)) 1)
        )
        (then 
            (sow
                #2
                if:(and 
                    (is In (to) (sites Next "Home")) 
                    (or
                        (= (count at:(to)) 2) 
                        (= (count at:(to)) 4)
                    )
                )
                apply:(fromTo 
                    (from (to)) 
                    (to (handSite Mover)) 
                    count:(count at:(to))
                ) 
                backtracking:True
            )
        )
    )
)

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

(game "Um el-Bagara" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 5 store:None
            { 
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "4,W,N,E" loop:True)
            }
        )
        (regions "Home1" P1 (sites Bottom))    
        (regions "Home2" P2 (sites Top))    
        (regions "Left1" P1 (sites {0..2}))   
        (regions "Left2" P2 (sites {9..7}))   
        (regions "Right1" P1 (sites {2..4}))   
        (regions "Right2" P2 (sites {7..5}))   
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 5 to:(sites Track))) 
        
        (play (or 
                ("Sow" "Left" "TrackCW")
                ("Sow" "Right" "TrackCCW")
            )
        )
        
        (end ("MancalaByScoreWhen" (all Passed)))
    )
)

