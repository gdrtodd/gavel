(define "SurroundedOf"
    (surround 
        (from (last To)) 
        Orthogonal 
        (between 
            if:(is #1 (who at:(between))) 
            (apply #2)
        ) 
        (to 
            if:(is Occupied (to))
        )
    )
)

(define "ThreeSlideInARow" (= (counter) 5))

(define "SurroundedOver" (is Triggered "Surrounded" #1))

(define "SurroundedMySelf" ("NoSites" (sites Around (last To) if:(is Empty (to)))))

(game "Susan" 
    (players 2) 
    (equipment { 
        (board (hex 5)) 
        (piece "Marker" Each "StepOrthogonalToEmpty")
    }) 
    
    (rules 
        (play (or
                (move Add 
                    (to (sites Empty)) 
                    (then (set Counter)) 
                )
                (forEach Piece)
                (then 
                    (priority 
                        ("SurroundedOf" Friend (trigger "Surrounded" (mover)))
                        ("SurroundedOf" Enemy (trigger "Surrounded" (next)))
                    )
                )
            )
        )
        
        (end {
            (if "ThreeSlideInARow" (result Mover Draw))
            (if "SurroundedMySelf" (result Next Win))
            (if ("SurroundedOver" P1) (result P2 Win)) 
            (if ("SurroundedOver" P2) (result P1 Win)) 
        }) 
    )
)

