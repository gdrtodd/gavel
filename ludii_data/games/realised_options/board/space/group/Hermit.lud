(define "CombinedGroupCounts"
    (
        + {
        (* 10000 (count Groups Orthogonal #1))
        (* 100 (- (count Groups Orthogonal #1) (count Groups Orthogonal #1 min:3)))
        (- 
            (- (count Groups Orthogonal #1) (count Groups Orthogonal #1 min:5))
            (- (count Groups Orthogonal #1) (count Groups Orthogonal #1 min:3))   
        )
        } 
    )
)

(game "Hermit" 
    (players 2) 
    (equipment { 
        (board (tri Hexagon 5) use:Vertex) 
        (piece "Marker" Each) 
        (piece "Marker" Neutral)
    }) 
    (rules
        (start {
            
            (set Score P1 0)
            (set Score P2 0)
        }) 
        
        (play
            (do
                (move Add (to (sites Empty)))
                ifAfterwards:
                (or {
                    (= 1 (size Group at:(last To) Orthogonal))
                    (= 3 (size Group at:(last To) Orthogonal))
                    (= 5 (size Group at:(last To) Orthogonal))
                    }
                )
                (then
                    (addScore Mover (- ("CombinedGroupCounts" if:(= (who at:(to)) (mover))) (score Mover)))
                )   
            )
        )
        (end
            (if (all Passed) 
                {
                (if (> ("CombinedGroupCounts" if:(= (who at:(to)) P1))
                        ("CombinedGroupCounts" if:(= (who at:(to)) P2))
                ) (result P1 Win))
                (if (< ("CombinedGroupCounts" if:(= (who at:(to)) P1))
                        ("CombinedGroupCounts" if:(= (who at:(to)) P2))
                ) (result P2 Win))
                }
                (result Mover Win)
            ) 
        )
    )
)

