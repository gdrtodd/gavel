(define "DistInColumn" (count Sites in:(difference (sites Column (column of:(from))) (sites Empty))))

(define "DistInRow" (count Sites in:(difference (sites Row (row of:(from))) (sites Empty))))

(define "DistInNESW" (count Sites in:(difference (sites Direction from:(from) (directions {NE SW}) included:True) (sites Empty))))

(define "DistInNWSE" (count Sites in:(difference (sites Direction from:(from) (directions {NW SE}) included:True) (sites Empty))))

(define "MoveTo" 
    (move Hop
        #1 
        (between 
            (exact (- #2 1))
            if:(not ("IsEnemyAt" (between)))
        )
        (to 
            if:(not ("IsFriendAt" (to)))
            (apply (if ("IsEnemyAt" (to)) (remove (to))))
        )
    )
)

(game "Lines of Action" 
    (players 2) 
    
    (equipment { 
        (board (square 8)) 
        (piece "Disc" Each 
            (or {
                ("MoveTo" (directions {N S}) "DistInColumn")
                ("MoveTo" (directions {W E}) "DistInRow")
                ("MoveTo" (directions {NE SW}) "DistInNESW")
                ("MoveTo" (directions {NW SE}) "DistInNWSE")
            })
        )
    }) 
    
    (rules 
        (start { 
            (place "Disc1" (difference (union (sites Top) (sites Bottom)) (sites Corners)))
            (place "Disc2" (difference (union (sites Right) (sites Left)) (sites Corners)))
        })
        
        (play (forEach Piece))
        
        (end { 
            (if (and (= 1 (count Groups if:(= (who at:(to)) P1))) (= 1 (count Groups if:(= (who at:(to)) P2)))) (result Mover Draw)) 
            (if (= 1 (count Groups if:(= (who at:(to)) P1))) (result P1 Win)) 
            (if (= 1 (count Groups if:(= (who at:(to)) P2))) (result P2 Win)) 
        }) 
    )
)

