(game "RootZone" 
    (players 2)
    (equipment {
        (board (tri {4 6 4 7 4}) use:Vertex)
        (piece "Disc" Each)
        (piece "Disc" Neutral)
    })
    (rules 
        (play
            (if 
                (= -1 (counter)) 
                (or 
                    (move Pass)
                    (move Add (piece "Disc0") (to (sites Board)))
                )
                (or
                    {
                    (if (= 1 (counter))
                        (move Select 
                            (from (sites Occupied by:Enemy)) 
                            (then 
                                (remove (last To) 
                                    (then (add (to (last To))))
                                )
                            ) 
                        )
                    ) 
                    (move Pass)
                    (move Add
                        (to 
                            (difference
                                (union
                                    (sites Outer)        
                                    (sites Around (sites Occupied by:Mover)) 
                                )
                                (union { 
                                    (sites Around (sites Occupied by:Enemy)) 
                                    (sites Around "SitesTooDense")   
                                    (sites Around (sites Occupied by:Neutral)) 
                                })
                            )
                            if:(and
                                (is Empty (to)) 
                                (>=    
                                    6 
                                    (count Pieces Mover in:(sites Around (to))) 
                                )
                            )
                        )
                        (then "ScoreUpdate")
                    )
                    }
                )
            ) 
        )
        (end 
            (if (all Passed) 
                (byScore)
            )
        )
    )
)

(define "SitesTooDense"
    (forEach 
        (sites Occupied by:Mover) 
        if:(<= 
            3 
            (count Pieces Mover in:(sites Around (site))) 
        )
    )
)

(define "ScoreUpdate"
    (if 
        (> (* 2 (size Group at:(last To))) (score Mover))
        (if 
            (> (* 2 (size Group at:(last To))) (score Next))
            (set Score Mover (+ 1 (* 2 (size Group at:(last To)))))
            (set Score Mover (* 2 (size Group at:(last To)))) 
        )
    )
)

