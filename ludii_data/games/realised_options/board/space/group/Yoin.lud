(define "UpdateScore"
    (do
        (set Var "CutOff" 0)
        next:(do
            (while 
                (and
                    (= 0
                        (-
                            (count Groups if:(= (id P1) (who at:(to))) min:(var "CutOff"))
                            (count Groups if:(= (id P2) (who at:(to))) min:(var "CutOff"))
                        )
                    )
                    (!= 0
                        (+
                            (count Groups if:(= (id P1) (who at:(to))) min:(var "CutOff"))
                            (count Groups if:(= (id P2) (who at:(to))) min:(var "CutOff"))
                        )
                    )
                )
                (set Var "CutOff"
                    (+ 1 (var "CutOff"))
                )
            )
            next:(and
                (set Score P1 (count Groups if:(= (id P1) (who at:(to))) min:(var "CutOff")))
                (set Score P2 (count Groups if:(= (id P2) (who at:(to))) min:(var "CutOff")))
            )
        )
    )
)

(game "Yoin"
    (players 2)
    (equipment {
        (board (hex 7))
        (piece "Disc" Each)
    })
    (rules
        (meta (swap))
        (play
            (move Add
                (to (sites Empty))
                (then (if (is Full) ("UpdateScore")))
            )
        )
        (end 
            (if (is Full) 
                {(if
                    (< 
                        (score P1)
                        (score P2)
                    )
                    (result P1 Win)
                )
                (if
                    (> 
                        (score P1)
                        (score P2)
                    )
                    (result P2 Win)
                )}
            )
        )
    )
)

