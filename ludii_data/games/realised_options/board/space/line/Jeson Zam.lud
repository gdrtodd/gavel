(game "Jeson Zam"
    (players 2)
    
    (equipment { 
        (board
            (add
                (remove
                    (add
                        (merge {
                            (rotate 30 (tri 2))
                            (shift 0.2 0 (rotate -30 (tri 2)))
                        })
                        vertices:{{0.6 0.3}} 
                    )
                    edges:{{0 2} {0 1} {1 2} {3 5} {5 4} {4 3}}
                )
                vertices:{
                {0.6 0.625} 
                {0.915 0.3} 
                {0.285 0.3} 
                {0.6 -0.01} 
                {0.285 0.1725} 
                {0.285 0.4375} 
                {0.915 0.4375} 
                {0.915 0.1725} 
                {0.75 0.075} 
                {0.45 0.075} 
                {0.75 0.535} 
                {0.45 0.535} 
                }
                edges:{
                {1 13} {13 17} {17 7} {7 2} 
                {2 12} {12 9} {9 11} {11 0}
                {5 13} {13 8} {8 14} {14 4}
                {1 14} {14 15} {15 10} {10 0}
                {5 7} {7 18} {18 12} {12 3}
                {3 11} {11 16} {16 10} {10 4}
                {0 16} {16 6} {6 17} {17 5}
                {4 15} {15 6} {6 18} {18 2}
                {3 9} {9 6} {6 8} {8 1}
                }
            )
            use:Vertex
        ) 
        (hand Each)
        (piece "Marker" Each 
            ("StepToEmpty" ~ 
                (then ("ReplayIfLine3")) 
            )
        ) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:9))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    ("RemoveAnyEnemyPieceNotInLine3" Orthogonal) 
                    (move 
                        (from (handSite Mover)) 
                        (to (difference (sites Empty) (sites Centre))) 
                        (then ("ReplayIfLine3")) 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    ("RemoveAnyEnemyPieceNotInLine3" Orthogonal) 
                    (forEach Piece)
                )
            )
        )
        }
        
        (end ("CaptureAll" Next))
    )
)

