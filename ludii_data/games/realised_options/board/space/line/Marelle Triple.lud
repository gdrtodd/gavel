(define "IfLine3MoveAgain" 
    (then 
        (if 
            (or { 
                (is Line 3 N exact:True) (is Line 3 S exact:True) 
                (is Line 3 E exact:True) (is Line 3 W exact:True) 
            })
            (moveAgain)
        )
    )
)

(game "Marelle Triple"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each ("StepToEmpty" ~ "IfLine3MoveAgain")) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:9))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    ("RemoveAnyEnemyPiece")
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    ("RemoveAnyEnemyPiece")
                    (if (> (count Pieces Mover) 4)
                        (forEach Piece)
                        (move 
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
        )
        }
        (end ("HavingLessPiecesLoss" Next 2))
    )
)

