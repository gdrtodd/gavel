(define "Nbors" 
    (count Sites in:(intersection
            (sites Around #1)
            (sites Occupied by:#2) 
        )
    )
)

(game "Trike"
    (players 2)
    (equipment {
        (board (hex Triangle 13))
        (piece "Marker" Each)
    })
    
    (rules
        (meta (swap))
        
        phases:{
        (phase "Opening" P1
            (play
                (move Add 
                    (to (sites Empty)) 
                    (then (set Var (last To)))
                )
            )
            (nextPhase)
        )
        
        (phase "Play"
            (play
                (move Add 
                    (to (sites LineOfSight Empty at:(var)))
                    (then (set Var (last To)))
                )
            )
        )
        }
        
        (end
            (if
                (no Moves Next)
                (byScore {
                    (score P1 (+ (if (is Mover P1) 1 0) ("Nbors" (last To) P1)))
                    (score P2 (+ (if (is Mover P2) 1 0) ("Nbors" (last To) P2)))
                })
            )
        )
    )
)

