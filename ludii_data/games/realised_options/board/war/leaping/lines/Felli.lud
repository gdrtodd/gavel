(game "Felli" 
    (players 2) 
    (equipment { 
        (board 
            (graph
                vertices:{ {0 0} {2 0} {4 0} {1 1} {2 1} {3 1} {2 2} {1 3} {2 3} {3 3} {0 4} {2 4} {4 4}}
                edges:{ {0 1} {0 3} {1 2} {1 4} {2 5} {3 4} {3 6} {4 5} {4 6} {5 6} {6 7} {6 8} {6 9} {7 8} {7 10} {8 9} {8 11} {9 12}
                {10 11} {11 12}}
            )
            use:Vertex
        )
        
        (piece "Counter" Each)
        (piece "DoubleCounter" Each)
        (regions P1 (intersection (sites Corners) (sites Bottom)))
        (regions P2 (intersection (sites Corners) (sites Top)))
    }) 
    
    (rules 
        (start {
            (place "Counter1" (expand (sites Bottom)))
            (place "Counter2" (expand (sites Top)))
        })
        
        (play 
            (or {
                (forEach Piece "Counter" ("HopCapture") 
                    (then ("PromoteIfReach" (sites Next) "DoubleCounter"))
                )
                (forEach Piece "Counter" ("StepToEmpty")
                    (then ("PromoteIfReach" (sites Next) "DoubleCounter"))
                )
                (forEach Piece "DoubleCounter" (move Slide))
                (forEach Piece "DoubleCounter" ("HopCapture"))
            })
        )
        
        (end ("NoMoves" Loss))
    )
)

