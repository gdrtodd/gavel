(define "HopCaptureForwards" 
    ("HopCapture" ~ (directions {Forwards Rightward Leftward} bySite:True))
)

(define "StepToEmptyForwards"
    ("StepToEmpty" (directions {Forwards Rightward Leftward} bySite:True))
)

(define "MoveChoice" 
    (or 
        (forEach Piece "Counter" #1
            (then ("PromoteIfReach" (sites Mover) "DoubleCounter"))
        )
        (forEach Piece "DoubleCounter" #2)
    )
)

(game "Game of Solomon" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board
            (renumber 
                (splitCrossings (add (regular Star 6) edges:{{0 5} {1 4} {2 3}}))
            )
            use:Vertex
        ) 
        (piece "Counter" Each) 
        (piece "DoubleCounter" Each)
        (regions "Palace1" P1 (sites {"E7"})) 
        (regions "Palace2" P2 (sites {"E1"})) 
    }) 
    
    (rules 
        (start {
            (place "Counter1" (sites {"E1" "A2" "D2" "E2" "F2" "I2"}))
            (place "Counter2" (sites {"E7" "A6" "D6" "E6" "F6" "I6"}))
        })
        
        (play
            (priority {
                ("MoveChoice" "HopCaptureForwards" "HopCapture")
                ("MoveChoice" "StepToEmptyForwards" "StepToEmpty")
            })
        )
        
        (end {
            ("BlockWin")
            (if 
                (and {
                    (= 1 (count Pieces P1)) 
                    (= 1 (count Pieces P2)) 
                    (= 1 (count Pieces P1 "DoubleCounter")) 
                    (= 1 (count Pieces P2 "DoubleCounter")) 
                }) 
                (result Mover Draw)
            )
        }) 
    )
)

