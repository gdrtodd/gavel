(define "HopMan" 
    (move 
        Hop 
        (from #1) 
        (between 
            if:(and (not (is In (between) (sites ToClear))) ("IsEnemyAt" (between)))
            (apply (remove (between) at:EndOfTurn))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" ("IsPieceAt" "Counter" Mover (last To)))

(game "HexDame" 
    (players 2) 
    (equipment { 
        (board (rotate 90 (hex 5))) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S)
        (piece "DoubleCounter" Each)
        (regions P1 (union (sites Side SW) (sites Side SE)))
        (regions P2 (union (sites Side NW) (sites Side NE)))
    }) 
    
    (rules 
        (start {
            (place "Counter1" (expand (sites Bottom) steps:3))
            (place "Counter2" (expand (sites Top) steps:3))
        })
        (play 
            (if "SameTurn"
                (if "IsUnpromoted"
                    (max Moves
                        ("HopMan" (last To)
                            (then 
                                ("ReplayIfCanMove" ("HopMan" (last To)) 
                                    ("PromoteIfReach" (sites Next) "DoubleCounter")
                                ) 
                            ) 
                        )
                    )
                    (max Moves ("HopSequenceCaptureAgain" before:10 after:10 at:EndOfTurn))
                )
                (priority {
                    (max Moves
                        (or 
                            (forEach Piece "Counter"
                                ("HopMan" (from) 
                                    (then 
                                        ("ReplayIfCanMove" ("HopMan" (last To))
                                            ("PromoteIfReach" (sites Next) "DoubleCounter")
                                        ) 
                                    ) 
                                ) 
                            )
                            (forEach Piece "DoubleCounter" ("HopSequenceCapture" before:10 after:10 at:EndOfTurn))
                        )
                    )
                    (or 
                        (forEach Piece "Counter" 
                            ("StepToEmpty" (directions {Forward FL FR}))
                            (then ("PromoteIfReach" (sites Next) "DoubleCounter"))
                        )
                        (forEach Piece "DoubleCounter" (move Slide))
                    )
                })
            )
        )
        (end ("BlockWin"))
    )
)

