(game "Chaturanga (al-Adli)"
    ("TwoPlayersNorthSouth") 
    (equipment {
        (board (square 8))
        (piece "King_noCross" ("StepToNotFriend"))
        ("ChessRook" "Rook")
        (piece "Elephant"
            (move Hop Orthogonal 
                (between (exact 1) if:True) 
                (to if:(not ("IsFriendAt" (to)))
                    (apply 
                        (if ("IsEnemyAt" (to))
                            (remove (to))
                        )
                    ) 
                )
            )
        )
        ("ChessKnight" "Knight")
        ("ChessPawn" "Pawn")
        (piece "Queen" ("StepToNotFriend" Diagonal))
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Elephant1" {"A1" "H1"}) (place "Rook1" {"B1" "G1"}) (place "Knight1" {"C1" "F1"}) (place "Queen1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Elephant2" {"A8" "H8"}) (place "Rook2" {"B8" "G8"}) (place "Knight2" {"C8" "F8"}) (place "Queen2" coord:"E8") (place "King_noCross2" coord:"D8") 
        })
        
        (play 
            (do (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            (if (= (count Pieces Next) 1) (result Mover Loss)) 
        })
    )
)

