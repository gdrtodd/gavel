(define "SitesNextToKing" (sites {"C1" "E1" "D8" "F8"}))
(define "KingSites" (sites {"E8" "D1"}))
(define "RememberRookMoved" (set State at:#1 1))
(define "RookMovedBefore" (= 1 (state at:#1)))

(define "KingMovedBefore" (= 1 (state at:#1)))
(define "RememberKingMoved" (set State at:#1 1))

(game "Main Chator (Batak)"
    ("TwoPlayersNorthSouth") 
    (equipment {
        (board (square 8))
        (piece "King_noCross"
            (or
                ("StepToNotFriend")	
                (if (not ("KingMovedBefore" (from)))
                    (or {
                        (move Hop
                            Orthogonal
                            (between
                                (exact 2)
                                if:True
                            )
                            (to 
                                if:(not ("IsFriendAt" (to))) 
                                (apply 
                                    (if ("IsEnemyAt" (to))
                                        (remove 
                                            (to) 
                                        )
                                    )
                                )
                            ) 
                        )
                        ("LeapCapture" "KnightWalk")
                        (if (is In (from) ("KingSites"))
                            (or
                                (if (and {
                                        (is Empty (ahead (from) steps:2 W))
                                        ("IsPieceAt" "Rook" Mover (ahead (from) W))
                                        (not ("RookMovedBefore" (ahead (from) W)))
                                    })
                                    (move
                                        (from)
                                        (to (ahead (from) steps:2 W))
                                    )
                                )
                                (if (and {
                                        (is Empty (ahead (from) steps:2 E))
                                        ("IsPieceAt" "Rook" Mover (ahead (from) E))
                                        (not ("RookMovedBefore" (ahead (from) E)))
                                    })
                                    (move
                                        (from)
                                        (to (ahead (from) steps:2 E))
                                    )
                                )
                            )
                        )
                        }
                        (then ("RememberKingMoved" (last To)))
                    )
                )
            )
        )
        ("ChessRook" "Rook" ~
            (then
                (if (and 
                        (not ("RookMovedBefore" (last To)))
                        (not (is In (last To) ("SitesNextToKing")))
                    )
                    ("RememberRookMoved" (last To))
                )
            )
        )
        ("ChessBishop" "Bishop_noCross")
        ("ChessKnight" "Knight")
        (piece "Pawn"
            (or {
                (if (and (= 0 (state at:(last To))) (is In (from) (sites Start (piece (what at:(from))))))
                    ("DoubleStepForwardToEmpty")
                )
                ("StepToEmpty" (if (= 1 (state at:(from))) Backward Forward))
                ("StepToEnemy" (directions {FR FL}))
                }
                (then
                    (if (is In (last To) (union (sites Bottom) (sites Top))) 
                        (if (= 0 (state at:(last To)))
                            (set State at:(last To) 1)
                            (set State at:(last To) 2)
                        )
                    )
                )
            )
        ) 
        ("ChessQueen" "Queen")
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Bishop_noCross1" {"C1" "F1"}) (place "Queen1" coord:"E1") (place "King_noCross1" coord:"D1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Bishop_noCross2" {"C8" "F8"}) (place "Queen2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        
        (play 
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            ("DrawIfNoMoves" Next)
        })
    )
)

