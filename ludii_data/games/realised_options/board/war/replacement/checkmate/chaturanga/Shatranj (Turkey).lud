(define "MovedBefore" (= 1 (state at:#1)))
(define "RememberMoved" (set State at:#1 1))

(game "Shatranj (Turkey)" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        ("ChessKing" "King_noCross")
        (piece "Ferz_noCross"
            (or
                ("StepToNotFriend" Diagonal)
                (if (not ("MovedBefore" (from)))
                    (move Hop 
                        (between if:True)
                        (to if:(is Empty (to)))
                    )
                )
                (then 
                    (if (not ("MovedBefore" (last To)))
                        ("RememberMoved" (last To))
                    )
                )
            )
        )
        (piece "Elephant"
            (move Hop 
                Diagonal
                (between if:True)
                (to if:(not ("IsFriendAt" (to)))
                    (apply 
                        (if ("IsEnemyAt" (to))
                            (remove (to))
                        )
                    ) 
                )
            )	
        )
        ("ChessKnight" "Knight")
        ("ChessRook" "Rook")
        ("ChessPawn" "Pawn" ~ (then ("PromoteIfReach" (sites Mover "Promotion") "Ferz_noCross")))
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        (play 
            (do (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            (if (no Moves Mover) (result Mover Loss))
        })
    )
)

