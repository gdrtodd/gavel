(game "Tsatsarandi"
    (players 2)
    (equipment {
        (board (square 8))
        ("ChessPawn" "Pawn")
        ("ChessRook" "Rook")
        ("ChessKing" "King_noCross")
        (piece "Bishop_noCross"
            (move Hop 
                Diagonal 
                (between if:True) 
                (to 
                    if:(or 
                        (is Empty (to)) 
                        ("IsEnemyAt" (to))
                    ) 
                    (apply (remove (to))) 
                )
            )		
        )
        ("ChessKnight" "Knight")
        (piece "Ferz_noCross" ("StepToNotFriend" Diagonal))
    })
    (rules 
        (start {
            (place "King_noCross1" coord:"E1") (place "King_noCross2" coord:"E8")
            (place "Ferz_noCross1" coord:"D1") (place "Ferz_noCross2" coord:"D8")
            (place "Bishop_noCross1" {"C1" "F1"}) (place "Bishop_noCross2" {"C8" "F8"})
            (place "Knight1" {"B1" "G1"}) (place "Knight2" {"B8" "G8"})
            (place "Rook1" {"A1" "H1"}) (place "Rook2" {"A8" "H8"})
            (place "Pawn1" (sites Row 1)) (place "Pawn2" (sites Row 6))
        })
        (play 
            (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King_noCross" Mover)))
        )
        
        (end ("Checkmate" "King_noCross"))
    )
)

