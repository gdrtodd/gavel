(define "CaptureForwardDiagonal"
    (move
        Step 
        #1
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "PromoteTo" 
    (move Promote (last To) (piece #1) Mover)
)

(define "StepForwardToEmpty"
    (move Step 
        #1
        Forward
        (to if:(is Empty (to)))
    )
)

(define "PlayAPiece"
    (or
        (forEach Die
            (if (= (pips) 6)
                (forEach Piece "King_noCross")
                (if (= (pips) 5)
                    (forEach Piece "Queen")
                    (if (= (pips) 4)
                        (forEach Piece "Rook")
                        (if (= (pips) 2)
                            (forEach Piece "Knight")
                            (if (= (pips) 3)
                                (forEach Piece "Bishop")
                                (if (= (pips) 1)
                                    (forEach Piece "Pawn")
                                )
                            )
                        )
                ))
            )
        )
        
        (forEach Site 
            (sites Direction from:(sites Mover) (if (is Mover P1) S N) distance:1)
            (if (= (what at:(site)) (id "Pawn" Mover))
                (or
                    (if (can Move ("CaptureForwardDiagonal" (from (site))))
                        ("CaptureForwardDiagonal" (from (site)))
                    )
                    (if (can Move ("StepForwardToEmpty" (from (site))))
                        ("StepForwardToEmpty" (from (site)))
                    )
                    (then
                        (if (is In (last To) (sites Mover "Promotion"))
                            (moveAgain) 
                        )
                    )
                )
            )
        )
    )
)

(define "NoEnemyKings" (no Pieces Next "King_noCross"))

(game "Dice Chess" 
    (players {(player N) (player S) }) 
    (equipment { 
        (board (square 8))
        (dice d:6 from:1 num:1)
        
        (piece "Pawn" Each
            (or {
                (if (is In (from) (sites Start (piece (what at:(from)))))
                    ("DoubleStepForwardToEmpty")
                )
                ("StepForwardToEmpty") 
                ("CaptureForwardDiagonal")
                }
                (then ("ReplayInMovingOn" (sites Mover "Promotion")))
            )
        )
        ("ChessBishop" "Bishop")
        ("ChessKnight" "Knight")
        ("ChessRook" "Rook")
        ("ChessQueen" "Queen")
        ("ChessKing" "King_noCross")
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    }) 
    
    (rules 
        
        (start { 
            (place "Pawn1" {"A2" "B2" "C2" "D2" "E2" "F2" "G2" "H2"})
            (place "Pawn2" {"H7" "G7" "E7" "F7" "D7" "C7" "B7" "A7"})
            (place "Bishop1" {"C1" "F1"}) (place "Bishop2" {"C8" "F8"}) 
            (place "Knight1" {"B1" "G1"}) (place "Knight2" {"G8" "B8"}) 
            (place "Rook1" {"A1" "H1"}) (place "Rook2" {"H8" "A8"}) 
            (place "Queen1" coord:"D1") (place "Queen2" coord:"D8")			
            (place "King_noCross1" coord:"E1") (place "King_noCross2" coord:"E8") 
        })
        
        (play 
            (do (if ("NewTurn") (roll))
                next:(if ("SameTurn")
                    (if (= (count Pips) 1)
                        (or {
                            ("PromoteTo" "Bishop")
                            ("PromoteTo" "Knight")
                            ("PromoteTo" "Rook")
                            ("PromoteTo" "Queen")
                            ("PromoteTo" "King_noCross")
                        })
                        (if (= (count Pips) 2)
                            ("PromoteTo" "Knight")
                            (if (= (count Pips) 3)
                                ("PromoteTo" "Bishop")
                                (if (= (count Pips) 4)
                                    ("PromoteTo" "Rook")
                                    (if (= (count Pips) 5)
                                        ("PromoteTo" "Queen")
                                        ("PromoteTo" "King_noCross")
                                    )
                                )
                            )
                        )
                    )
                    "PlayAPiece"
                )
            )
        )
        
        (end 
            (if "NoEnemyKings"
                (result Next Loss)
            )
        )
    )
)

