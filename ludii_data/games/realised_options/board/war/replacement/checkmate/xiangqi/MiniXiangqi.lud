(define "JiangSameColumnWithNoPieceBetween"
    (if (= (column of:(where "Jiang" P1)) (column of:(where "Jiang" P2)))
        (not ("NoSites" (forEach (sites Direction from:(where "Jiang" P1) N stop:(= (to) (where "Jiang" P2))) if:(is Occupied (site)))))
        True
    )
)

(define "StepMoveOnThePalace" 
    (move Step
        #1 
        (to 
            if:(and 
                (is In (to) (sites Mover "Palace")) 
                (not ("IsFriendAt" (to)))
            ) 
            (apply (remove (to)))
        )
    ) 
)

(game "MiniXiangqi" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (rectangle 7 7) use:Vertex) 
        
        (piece "Jiang" Each ("StepMoveOnThePalace" Orthogonal)) 
        
        (piece "Ma" Each 
            (forEach Direction 
                Orthogonal 
                (to 
                    if:(is Empty (to)) 
                    (apply 
                        (forEach Direction 
                            (from (to)) 
                            (directions {FR FL} of:All) 
                            (if (not ("IsFriendAt" (to))) 
                                (move 
                                    (from) 
                                    (to 
                                        (apply 
                                            if:("IsEnemyAt" (to))
                                            (remove (to))
                                        )
                                    )
                                )
                            )
                        ) 
                    )
                )
            ) 
        )
        
        (piece "Ju" Each ("SlideCapture" Orthogonal))
        
        (piece "Pao" Each 
            (or 
                (move Slide Orthogonal) 
                (move Hop
                    Orthogonal 
                    (between 
                        before:(count Rows) 
                        after:(count Rows) 
                        if:(is Occupied (between))
                    )
                    (to 
                        if:(is Empty (to)) 
                        (apply 
                            if:("IsEnemyAt" (to)) 
                            (remove (to))
                        )
                    ) 
                ) 
            )
        )
        
        (piece "Zu" Each ("StepToNotFriend" (directions {Forward Rightward Leftward})))
        
        (regions "PalaceP1" P1 { 2..4 9..11 16..18 })
        (regions "PalaceP2" P2 { 30..32 37..39 44..46 })
    }) 
    
    (rules 
        (start { 
            (place "Jiang1" coord:"D1") (place "Ma1" {"C1" "E1"}) (place "Ju1" {"A1" "G1"}) (place "Pao1" {"B1" "F1"}) (place "Zu1" {"A2" "C2" "D2" "E2" "G2"})
            (place "Jiang2" coord:"D7") (place "Ma2" {"C7" "E7"}) (place "Ju2" {"A7" "G7"}) (place "Pao2" {"B7" "F7"}) (place "Zu2" {"A6" "C6" "D6" "E6" "G6"})
        })
        
        (play (do 
                (forEach Piece) 
                ifAfterwards:(and 
                    ("JiangSameColumnWithNoPieceBetween")
                    (not ("IsInCheck" "Jiang" Mover))
                )
            )
        )
        
        (end {
            ("Checkmate" "Jiang")
            ("BlockWin")
        })
    )
)

