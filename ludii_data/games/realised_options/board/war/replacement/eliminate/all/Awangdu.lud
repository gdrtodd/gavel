(define "AllowToBegin" (is In (count Pips) (sites {0 1 5 6})))

(define "CanMove" (= (value Player Mover) 1))

(define "RemovePiece" 
    (apply 
        (if 
            ("IsEnemyAt" (to))
            (remove (to))
        )
    )
)

(game "Awangdu"
    (players 2) 
    (equipment { 
        (board (rectangle 5 12) 
            { 
            (track "Track1" "0,E,N1,W,N1,E,N1,W,N1,E" P1 directed:True)
            (track "Track2" "59,W,S1,E,S1,W,S1,E,S1,W" P2 directed:True)
            }
            use:Vertex
        )
        ("StickDice" 6)
        (piece "Disc" Each
            (if (not ("IsFriendAt" ("NextSiteOnTrack" (mapEntry (var)))))
                (move 
                    (from) 
                    (to 
                        ("NextSiteOnTrack" (mapEntry (var)))
                        ("RemovePiece")
                    )
                )
            )
        ) 
        (map {(pair 0 10) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 1) (pair 6 10)})
    }) 
    
    (rules 
        (start { 	
            (place "Disc1" (sites Bottom))
            (place "Disc2" (sites Top))
        })	
        
        (play
            ("RollMove"
                (if 
                    (or 
                        ("CanMove") 
                        ("AllowToBegin")
                    )
                    (do (set Var (count Pips))
                        next:(forEach 
                            Piece 
                            (then (if (not ("CanMove")) (set Value Mover 1)))
                        )
                    )
                )
            )
        )
        
        (end ("HavingLessPiecesLoss" Next 2))
    )
)

