(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "EntryPoint" (mapEntry "Entry" (mover)))

(define "NoPieceOnBoard" (< (count Pieces Mover in:(sites Board)) 1))

(define "SetCapturePiece"
    (set State #1 #2 2)
)

(define "SetCapturingPiece"
    (set State #1 #2 1)
)

(define "UnsetCapturingPiece" (set State #1 #2 0))

(define "UnsetCapturingPieces" 
    (forEach Level (last From) FromTop
        (if ("IsFriendAt" (last From) level:(level))
            ("UnsetCapturingPiece" at:(last From) level:(level))
        )
    ) 
)

(define "AtLeastAnEnemyPieceThere"
    (is In #1 (sites Occupied by:Enemy top:False))
)

(define "CaptureMove" 
    (forEach Level (last To) FromTop
        (and
            (if ("IsEnemyAt" (last To) level:(level))
                ("SetCapturePiece" at:(last To) level:(level))
            )
            (if ("IsFriendAt" (last To) level:(level))
                ("SetCapturingPiece" at:(last To) level:(level))
            )
        )
    ) 
)

(define "CapturingPiece" (= 1 #1))

(define "CapturedPiece" (= 2 #1))

(define "FreePiece" (= 0 #1))

(define "EnterAPiece" 
    (move
        (from (handSite Mover))
        (to ("NextSiteOnTrack" (- "ThrowValue" 1) from:("EntryPoint" (mover)) "Track"))
    )
)

(define "RemoveCapturedPieces" 
    (forEach Level (last From) FromTop
        (if ("CapturedPiece" (state at:(last From) level:(level)))
            (remove (last From) level:(level))
        )
    )
)
(define "RebirthCapturingPiece"
    (add
        (piece (id "Stick" Mover))
        (to (handSite Mover))
    )
)

(define "CapturedPiecesFollowCapturingPiece"
    (forEach Level (last From) FromTop
        (if ("CapturedPiece" (state at:(last From) level:(level)))
            (fromTo 
                (from (last From) level:(level))
                (to (last To))
            )
        )
    )	
)

(game "Boolik"
    (players 6)
    (equipment {
        (board 
            (rectangle 1 15) 
            
            {
            (track "Track1" {0..13} loop:True P1)
            (track "Track2" {13..0} loop:True P2)
            (track "CaptureTrack1" {13..0} P1 directed:True)
            (track "CaptureTrack2" {0..13} P2 directed:True)
            (track "Track3" {0..13} loop:True P3)
            (track "Track4" {13..0} loop:True P4)
            (track "CaptureTrack3" {13..0} P3 directed:True)
            (track "CaptureTrack4" {0..13} P4 directed:True)
            (track "Track5" {0..13} loop:True P5)
            (track "Track6" {13..0} loop:True P6)
            (track "CaptureTrack5" {13..0} P5 directed:True)
            (track "CaptureTrack6" {0..13} P6 directed:True)
            } 
            
            use:Edge
        )
        (piece "Stick" Each
            (or
                (if ("FreePiece" (state at:(from) level:(level)))
                    (move 
                        (from (from) level:(level))
                        (to 
                            ("NextSiteOnTrack" ("ThrowValue") from:(from) "Track")
                        )
                        (then 
                            (if ("AtLeastAnEnemyPieceThere" (last To))
                                ("CaptureMove")
                            )
                        )
                    )
                )
                (if ("CapturingPiece" (state at:(from) level:(level)))
                    (if ("IsNotOffBoard" ("NextSiteOnTrack" ("ThrowValue") from:(from) "CaptureTrack"))
                        (move 
                            (from (from) level:(level))
                            (to 
                                ("NextSiteOnTrack" ("ThrowValue") from:(from) "CaptureTrack")
                            )
                            (then 
                                (and
                                    ("CapturedPiecesFollowCapturingPiece")
                                    ("UnsetCapturingPieces")
                                )
                            )
                        )
                        (move Remove (from) level:(level)
                            (then
                                (and {
                                    ("UnsetCapturingPieces")
                                    ("RemoveCapturedPieces")	
                                    ("RebirthCapturingPiece")
                                })
                            )
                        )
                    )
                )
            )
        )
        (regions "AllSites" (sites Board Vertex))
        (map "Throw" {(pair 0 5) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4)})
        (map "Entry" {
            (pair 1 0) (pair 2 13) (pair 3 0) (pair 4 13) 
            (pair 5 0) (pair 6 13) (pair 7 0) (pair 8 13) 
            (pair 9 0) (pair 10 13) (pair 11 0) (pair 12 13) 
            (pair 13 0) (pair 14 13) (pair 15 0) (pair 16 13)
        })
        ("StickDice" 4)
        (hand Each)
    })
    (rules
        (start {
            
            (set Team 1 {P1 P3 P5})
            (set Team 2 {P2 P4 P6})
            (place Stack "Stick1" (handSite P1) count:5)
            (place Stack "Stick2" (handSite P2) count:5)
            (place Stack "Stick3" (handSite P3) count:5)
            (place Stack "Stick4" (handSite P4) count:5)
            (place Stack "Stick5" (handSite P5) count:5)
            (place Stack "Stick6" (handSite P6) count:5)
            
        })
        (play 
            ("RollMove"
                (or {
                    (if (and ("NoPieceOnBoard") (is Occupied (handSite Mover)))
                        ("EnterAPiece")
                    )
                    (forEach Piece)
                })
                (then
                    (if (and (not (no Pieces Mover)) ("NewTurn"))
                        (moveAgain)
                    )
                )
            )
        )
        
        (end ("CaptureAllTeam"))
    )
)

