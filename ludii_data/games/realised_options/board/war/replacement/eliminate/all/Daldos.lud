(define "MoveAPiece" 
    (if (or
            (= (state at:(from)) 1) 
            (and (= (state at:(from)) 0) (= 1 (pips)))
        )
        (move Slide
            #1 
            (between 
                (exact (pips))
                if:(is Empty (between))
            ) 
            (to 
                if:("IsEnemyAt" (to)) 
                (apply if:(not ("IsFriendAt" (to))) (remove (to)))
            )
            (then
                (if (= (state at:(last To)) 0)
                    (set State at:(last To) 1)
                )
            )
        )
    )
)

(game "Daldos"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 16)
                (shift -1 1 (rectangle 1 17))
                (shift 0 2 (rectangle 1 16))
            })
            {
            (track "Track1" {0..15 32..16 33..48 32..16} loop:True P1)
            (track "Track2" {33..48 32..16 0..15 32..16} loop:True P2)
            }
            use:Vertex
        )
        (regions "AllSites" (sites Board))
        (dice d:4 num:2)
        (piece "Minus" P1 ("MoveAPiece" "Track1"))
        (piece "Minus" P2 ("MoveAPiece" "Track2"))
    })
    (rules 
        (start {
            (place "Minus1" (sites Bottom))
            (place "Minus2" (sites Top))
        })
        (play 
            ("RollEachNewTurnMove"
                (forEach Die 
                    replayDouble:(and (= (face 36) 1) (= (face 37) 1))
                    (forEach Piece
                        (then 
                            (if (not (all DiceUsed))
                                ("ReplayIfCanMove" (forEach Die (forEach Piece)))
                            )
                        )
                    )
                )
            )
        )
        (end ("CaptureAll" Next))
    )
)

