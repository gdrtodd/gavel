(define "ThrowValue" (count Pips))
(define "ExtraThrowValue" (mapEntry "ExtraThrow" (count Pips)))
(define "ActivatedPiece" (= (state at:#1) 1))
(define "ActivePiece" (set State at:#1 1))
(define "Sig"
    (or
        (= 1 ("ThrowValue"))
        (= 5 ("ThrowValue"))
    )
)
(define "CaptureMove"
    (if ("IsEnemyAt" (to))
        (remove (to))
    )
)

(define "MoveToActivate" 
    (move
        (from 
            (from)
            if:(and 
                (not ("ActivatedPiece" (from)))
                (or ("Sig") (= 1 (var "SpecialSig")))
            )	
        )
        (to 
            ("NextSiteOnTrack" 1)
            if:(not ("IsFriendAt" (to)))
            (apply "CaptureMove")
        )
        (then
            (and
                ("ActivePiece" (last To))
                (if 
                    (= 1 (var "SpecialSig")) 
                    (and {
                        (moveAgain)
                        (set Var (+ (var) 1))
                    })
                )
            )
        )
    )	
)

(define "Move"
    (move
        (from 
            (from)
            if:("ActivatedPiece" (from))
        )
        (to 
            ("NextSiteOnTrack" (if (= 1 (var "SpecialSig")) (+ 1 ("ThrowValue")) ("ThrowValue")))
            if:(not ("IsFriendAt" (to)))
            (apply "CaptureMove")
        )
    )	
)

(game "Sig (El Oued Capture)"
    (players 2)
    (equipment {
        (board
            (rectangle 3 6)
            {
            (track "Track1" "0,E,N1,W,N1,E,S1,W" loop:True P1)
            (track "Track2" "17,W,S1,E,S1,W,N1,E" P2 directed:True)
            }
        )
        (piece "Marker" Each)
        (hand Each)
        ("StickDice" 6)
        (map "ExtraThrow" {(pair 0 3) (pair 1 1) (pair 2 0) (pair 3 0) (pair 4 0) (pair 5 1) (pair 6 1)})
    })
    (rules
        (start { 
            (place "Marker1" (sites Bottom))
            (place "Marker2" (sites Top))
        })
        (play 
            ("RollMove"
                (priority 
                    (forEach Piece ("MoveToActivate"))
                    (forEach Piece ("Move"))
                )
                (then
                    (if (!= 0 ("ExtraThrowValue"))
                        (and
                            (if (= 3 ("ExtraThrowValue"))
                                (if (<= (var) 0)
                                    (set Var 2)
                                )
                            )
                            (if (!= (mover) (prev))
                                (and 
                                    (moveAgain)
                                    (if (!= 1 ("ThrowValue")) (set Var "SpecialSig" 1))
                                )
                            )
                        )
                        (if (> (var) 0)
                            (and {
                                (set Var (- (var) 1))
                                (moveAgain)
                            })
                            (set Var "SpecialSig" 0)
                        )
                    )
                )
            )
        )
        (end ("CaptureAll" Next))
    )	
)

