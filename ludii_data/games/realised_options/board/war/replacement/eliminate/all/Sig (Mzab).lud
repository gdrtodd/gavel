(define "ThrowValue" (mapEntry (count Pips)))

(define "ActivatePiece" (if (= (state at:(last To)) 1) (set State at:(last To) 0)))

(define "ActivatedPiece" (= (state at:(from)) 0))

(define "InactivePiece" (= (state at:(from)) 1))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (remove (to))
    ) 
)

(define "AllPieceInOpponentHome" 
    (and
        (= (count Sites in:(difference (sites Occupied by:P1) (sites P2))) 0)
        (= (count Sites in:(difference (sites Occupied by:P2) (sites P1))) 0)
    )		
)

(game "Sig (Mzab)"
    (players 2)
    (equipment {
        (board 
            (rectangle 4 20) 
            {
            (track "Track1" "0,E,N1,W,N1,E,N1,W" P1 directed:True)
            (track "Track2" "79,W,S1,E,S1,W,S1,E" P2 directed:True)
            }
            use:Vertex
        )
        (piece "Stick" Each
            (if (or 
                    ("ActivatedPiece") 
                    (and (or (= 1 ("ThrowValue")) (= 6 ("ThrowValue"))) ("InactivePiece"))
                )
                (if ("IsNotOffBoard" ("NextSiteOnTrack" (if ("InactivePiece") 1 ("ThrowValue"))))
                    (if (or
                            (is In ("NextSiteOnTrack" (if ("InactivePiece") 1 ("ThrowValue"))) (sites Empty))
                            ("IsEnemyAt" ("NextSiteOnTrack" (if ("InactivePiece") 1 ("ThrowValue"))))
                        )
                        (move 
                            (from)
                            (to 
                                ("NextSiteOnTrack" (if ("InactivePiece") 1 ("ThrowValue"))) 
                                "CaptureEnemyPiece"
                            )
                        )
                    )
                    (then ("ActivatePiece"))
                )
            )
        )
        (regions "AllSites" (sites Board))
        ("StickDice" 6)
        (map {(pair 0 6) (pair 1 0) (pair 2 2) (pair 3 1) (pair 4 1) (pair 5 0) (pair 6 6)})
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })
    (rules 
        (start { 
            (place "Stick1" (sites Bottom) state:1)
            (place "Stick2" (sites Top) state:1)
        })
        (play ("RollMove" (forEach Piece)))
        (end {
            ("CaptureAll" Next)
            (if ("AllPieceInOpponentHome")
                (result Mover Draw)
            )
        })
    )
)

