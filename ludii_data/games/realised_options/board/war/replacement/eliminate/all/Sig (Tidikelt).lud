(define "BottomSite" 36)

(define "ThrowValue" (mapEntry "Throw" (count Pips))) 

(define "Sig" (= 1 ("ThrowValue")))

(define "ActivatePiece" (set State at:#1 1))

(define "PieceActivated" (!= 0 (state at:#1)))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (if (is In (to) (sites Next "Home"))
            (remove (to))
            (if ("IsOffBoard" ("FirstAvailableInHome" Next)) 
                (remove (to))
                (fromTo
                    (from (to))
                    (to ("FirstAvailableInHome" Next))
                )
            )
        )
    ) 
)

(define "IsInTrack" (is In #1 (sites Track Mover #2)))

(define "FirstAvailableInHome" (min (array (intersection (sites Empty) (sites #1 "Home")))))

(define "CaptureEnemyPieceInEntering" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (if ("IsOffBoard" ("FirstAvailableInHome" Next)) 
            (remove (to))
            (fromTo
                (from (to))
                (to ("FirstAvailableInHome" Next))
            )
        )
    ) 
)

(define "Move"
    (or 
        (if ("IsInTrack" (from) "HomeTrack")
            (if 
                (if ("PieceActivated" (from))
                    True
                    ("Sig") 
                )
                (if (not ("IsFriendAt" ("NextSiteOnTrack" ("ThrowValue") "HomeTrack")))
                    (move 
                        (from)
                        (to 
                            ("NextSiteOnTrack" ("ThrowValue") "HomeTrack")
                            "CaptureEnemyPiece"
                        )
                        (then
                            (if (and (not ("PieceActivated" (last To))) ("Sig"))
                                ("ActivatePiece" (last To))
                            )
                        )
                    )
                )	
            )
        )
        (if ("IsInTrack" (from) "EnemyTrack")
            (if (not ("IsFriendAt" ("NextSiteOnTrack" ("ThrowValue") "EnemyTrack")))
                (move 
                    (from)
                    (to 
                        ("NextSiteOnTrack" ("ThrowValue") "EnemyTrack")
                        "CaptureEnemyPiece"
                    )
                )
            )	
        )
    )
)

(game "Sig (Tidikelt)"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 12 3) 
                (shift 1 -1 (rectangle 13 1)) 
            })
            {
            (track "HomeTrack1" "0,N,E1,S" P1 directed:True)
            (track "HomeTrack2" "2,N,W1,S" P2 directed:True)
            (track "EnemyTrack1" "2,N,W1,S" P1 directed:True)
            (track "EnemyTrack2" "0,N,E1,S" P2 directed:True)
            }
            use:Vertex
        )
        (piece "Stick" Each)
        (regions "AllSites" (sites Board))
        ("StickDice" 4)
        (map "Throw" {(pair 0 6) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4)})
        (map "FirstEnemySite" {(pair P1 2) (pair P2 0)})
        (regions "Home" P1 (sites Left))
        (regions "Home" P2 (sites Right))
    })
    (rules 
        (start { 
            (place "Stick1" (sites Left))
            (place "Stick2" (sites Right))
        })
        (play 
            ("RollMove"
                (or
                    (if (and ("Sig") (= (mover) (who at:("BottomSite"))))
                        (move 
                            (from ("BottomSite"))
                            (to 
                                (mapEntry "FirstEnemySite" (mover))
                                if:(not (is Friend (mapEntry "FirstEnemySite" (mover))))
                                "CaptureEnemyPieceInEntering"
                            )
                        )
                    )
                    (forEach Piece ("Move"))
                )
            )
        )
        (end ("CaptureAll" Next))
    )
)

