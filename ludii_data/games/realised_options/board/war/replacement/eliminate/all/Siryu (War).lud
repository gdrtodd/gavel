(define "ThrowValue" (mapEntry "ThrowDiceValue" (count Pips)))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" ("ThrowValue")))

(define "ActivatePiece" (if (= (state at:(last To)) 1) (set State at:(last To) 0)))

(define "ActivatedPiece" (= (state at:(from)) 0))

(define "InactivePiece" (= (state at:(from)) 1))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (remove (to))
    ) 
)

(define "PieceMove" 
    (if (or 
            ("ActivatedPiece") 
            (and (= (count Pips) (mapEntry "Sir" (mover))) ("InactivePiece"))
        )
        (if ("IsNotOffBoard" "SiteToMoveOnTrack")
            (if (or
                    (is In "SiteToMoveOnTrack" (sites Empty))
                    ("IsEnemyAt" "SiteToMoveOnTrack")
                )
                (move 
                    (from)
                    (to 
                        "SiteToMoveOnTrack" 
                        "CaptureEnemyPiece"
                    )
                )
            )
            (then ("ActivatePiece"))
        )
    )
)

(game "Siryu (War)"
    (players 2)
    (equipment {
        (board 
            (rectangle 4 12) 
            {
            (track "Track1" "0,E,N1,W,N1,E,23,E,W,N1,E,23,W,N1,E,23,W,N1,E,23,W,N1,E,23,W,N1,E,23,W,N1,E,23" loop:True P1)
            (track "Track2" "47,W,S1,E,S1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24" loop:True P2)
            }
            use:Vertex
        )
        (piece "Stick" P1 ("PieceMove"))
        (piece "Marker" P2 ("PieceMove"))
        (regions "AllSites" (sites Board))
        ("StickDice" 6)
        (map "ThrowDiceValue" {(pair 0 6) (pair 1 5) (pair 2 0) (pair 3 3) (pair 4 0) (pair 5 5) (pair 6 6)})
        (map "Sir" {(pair 1 1) (pair 2 5)})
    })
    (rules 
        (start { 
            (place "Stick1" (sites Bottom) state:1)
            (place "Marker2" (sites Top) state:1)
        })
        (play 
            ("RollMove" 
                (if (can Move (forEach Piece))
                    (forEach Piece (then (if (= ("ThrowValue") 5) (moveAgain))))
                    (move Pass (then (if (= ("ThrowValue") 5) (moveAgain))))
                )
            )
        )
        (end ("CaptureAll" Next))
    )
)

