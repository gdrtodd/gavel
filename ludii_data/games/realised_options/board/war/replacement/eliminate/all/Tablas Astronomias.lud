(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne" 
    (or 
        (and 
            ("IsEnemyAt" (to)) 
            (= (topLevel at:(to)) 0)
        ) 
        (not ("IsEnemyAt" (to)))
    )
)

(game "Tablas Astronomias"
    (players 7)
    (equipment {
        (board 
            (concentric {49})
            {
            (track "Track" {48 46 44 42 40 38 36 34 32 30 28 26 24 22 20 18 16 14 12 10 8 6 4 2 0 1 3 5 7 9 13 15 17 19 21 23 25 27 29 31 33 35 37 39 41 43 45 47} loop:True)
            }
            use:Vertex
        )
        (dice d:7 num:3)
        (piece "Disc" Each)
    })
    (rules 
        (start {
            (place Stack "Disc1" 14 count:7) 
            (place Stack "Disc2" 0 count:7) 
            (place Stack "Disc3" 13 count:7) 
            (place Stack "Disc4" 27 count:7) 
            (place Stack "Disc5" 41 count:7) 
            (place Stack "Disc6" 42 count:7) 
            (place Stack "Disc7" 28 count:7) 
        })
        (play
            ("RollEachNewTurnMove" 
                (forEach Die
                    if:("DieNotUsed")
                    (forEach Site
                        (sites Occupied by:Mover)
                        (move 
                            (from (site))
                            (to ("NextSiteOnTrack" (pips) from:(site))
                                if:("NotEnemyOrOnlyOne")
                                (apply 
                                    if:("IsEnemyAt" (to)) 
                                    (remove (to))
                                )
                            )
                        )
                    )
                    (then ("ReplayNotAllDiceUsed"))
                )
            )
        )
        
        (end (forEach NonMover if:(no Pieces Player) (result Player Loss)))
    )
)

