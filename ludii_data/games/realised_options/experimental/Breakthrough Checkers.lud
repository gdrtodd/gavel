(define "HopMan" 
    (move Hop 
        (from #1) 
        (directions {FR FL}) 
        (between 
            if:("IsEnemyAt" (between)) 
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "ReachedTarget" (is In (last To) (sites Mover)))

(game "Breakthrough Checkers" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S)
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
    }) 
    (rules 
        (start { 
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1) ))
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1)))
        })
        
        (play 
            (if "SameTurn"
                (or
                    ("HopMan" (last To)
                        (then 
                            ("ReplayIfCanMove" ("HopMan" (last To))) 
                        ) 
                    )
                    (move Pass)
                )
                (or 
                    (forEach Piece "Counter" 
                        ("HopMan" (from)
                            (then 
                                ("ReplayIfCanMove" ("HopMan" (last To))) 
                            )
                        ) 
                    )
                    (forEach Piece "Counter" 
                        ("StepToEmpty" (directions {FR FL})) 
                    )
                )
            )
        ) 
        
        (end (if "ReachedTarget" (result Mover Win))) 
    )
)

