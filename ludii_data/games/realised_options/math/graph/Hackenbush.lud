(define "RemoveCutBranch" 
    (forEach Site
        (sites Group Edge at:(site))
        (remove Edge (site))
    )
)

(define "NotConnectedToTheGround" 
    (= 0 
        (count Sites in:(intersection 
                ("Ground")
                (sites Group Edge at:(site) if:(is Occupied (to)))
            )
        )
    )
)

(define "Ground" (sites Bottom))

(game "Hackenbush" 
    (players 2) 
    (equipment { 
        (board 
            (graph
                vertices:{ 
                {0 0} {2 0} {4 0} {6 0} {8 0} {10 0} {15 0} {4 2} {4 4} {6 4} {3 5} {4 6} {10 2}
                {10 4} {8 4} {12 5} {10 6}}
                edges:{ 
                {0 1} {1 2} {2 3} {4 3} {4 5} {6 5} {2 7} {7 8} {7 9} {8 10} {8 11}
                {5 12} {12 14} {12 13} {13 15} {13 16}}
            )
        use:Edge)
        (piece "Marker" Each) 
        (piece "Disc" Shared) 
        (piece "Cross" Neutral) 
        (regions All (expand (sites Bottom)))  
    }) 
    
    (rules
        (start {
            (set Neutral Edge ("Ground")) 
            
            (set Shared Edge (difference (sites Board Edge) (sites {0..5})))       
            
        })  
        (play
            (move
                Remove (sites Occupied by:Shared)     
                (then		   		
                    (forEach Site 
                        (sites Incident Edge of:Edge at:(last To)) 
                        (if
                            ("NotConnectedToTheGround")
                            ("RemoveCutBranch")
                        )
                    )
                )
            )
        )
        (end      
            (if (no Moves Mover) (result Next Win))   
        )
    )
)

