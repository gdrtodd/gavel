(define "Max" (if (= 1 (count Turns)) (- 40 1) (* 2 (value Player (prev MoverLastTurn)))))

(define "NumRemove" (+ 1 (count MovesThisTurn)))

(game "Fibonacci Nim" 
    (players 2) 
    (equipment { 
        (board (square 1)) 
        (piece "Marker" Neutral)
    }) 
    (rules
        (start (set Count 40 at:0)) 
        
        (play 
            (if "SameTurn"
                (or
                    (if (<= "NumRemove" "Max")
                        (move Select 
                            (from (last To))
                            (then 
                                (if (= (count at:(last To)) 1)
                                    (remove (last To))
                                    (and (set Count at:(last To) (- (count at:(last To)) 1)) (moveAgain)) 
                                )
                            ) 
                        )
                    )
                    (move Pass (then (set Value Mover (- ("NumRemove") 1))))
                )
                (move Select
                    (from (sites Occupied by:Neutral))
                    (then 
                        (if (= (count at:(last To)) 1)
                            (remove (last To))
                            (and 
                                (set Count at:(last To) (- (count at:(last To)) 1)) 
                                (moveAgain)
                            ) 
                        )
                    ) 
                )
            )
        ) 
        (end (if (no Moves Next) (result Mover Win)))
    )
)

