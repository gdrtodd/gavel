(game "Nim" 
    (players 2) 
    (equipment { 
        (board (rectangle 1 5)) 
        (piece "Marker" Neutral)
    }) 
    (rules
        (start  		
            {
            (set Count 3 to:(sites {0 4}))
            (set Count 4 to:(sites {1 3}))
            (set Count 5 at:2)
            }
        ) 
        
        (play 
            (if "SameTurn"
                (or
                    (move Select 
                        (from (last To))
                        (then 
                            (if (= (count at:(last To)) 1)
                                (remove (last To))
                                (and (set Count at:(last To) (- (count at:(last To)) 1)) (moveAgain)) 
                            )
                        ) 
                    )
                    (move Pass)
                )
                (move Select
                    (from (sites Occupied by:Neutral))
                    (then 
                        (if (= (count at:(last To)) 1)
                            (remove (last To))
                            (and (set Count at:(last To) (- (count at:(last To)) 1)) (moveAgain)) 
                        )
                    ) 
                )
            )
        ) 
        (end (if (no Moves Next) (result Next Win)))
    )
)

