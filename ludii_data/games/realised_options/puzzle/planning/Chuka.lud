(define "Rumba" 0)

(define "AllSitesNotRumba" (difference (sites Board) "Rumba"))

(define "LastHole" (last To afterConsequence:True))

(define "AllCountersInRumba" (= 8 (count at:"Rumba")))

(game "Chuka"
    (players 1)
    (equipment {
        (board 
            (concentric {5}) 
            (track "Track" {0 1 3 4 2} loop:True)
            use:Vertex
        )
        (piece "Seed" Shared)
    })
    (rules
        (start (set Count 2 to:"AllSitesNotRumba"))
        (play 
            (move Select
                (from
                    (if 
                        (!= ("Rumba") (value Pending)) 
                        (sites {"LastHole"})
                        ("AllSitesNotRumba")
                    )
                    if:(!= 0 (count at:(from)))
                )
                (then
                    (sow 
                        apply:(if (> (count at:(to)) 1)
                            (set Pending (to))
                        )
                    )
                )
            )
        )
        (end {
            (if ("AllCountersInRumba") (result Mover Win))
            (if 
                (and (= 1 (count at:("LastHole"))) (!= ("LastHole") ("Rumba"))) 
                (result Mover Loss)
            )
        })
    )
)

