(define "NextSiteFrom" ("NextSiteOnTrack" #2 from:#1))

(define "AllPiecesInLastQuadrant" ("AllOwnedPiecesIn" (sites Mover "LastQuadrant")))

//------------------------------------------------------------------------------

(game "Buffa de Baldrac"
    (players 2)
    (equipment {
        ("TableBoard" ("TableTracksOpposite" End))
        (dice d:6 num:3)
        (piece "Disc" Each
            (or
                (forEach Die 
                    combined:True 
                    if:(and
                        ("DieNotUsed")
                        (<= 0 ("NextSiteFrom" (from) (pips)))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NoEnemyOrOnlyOne" (to))
                            ("HittingCapture" (handSite Next))
                        )
                    )
                    (then ("ReplayNotAllDiceUsed"))
                )
                (if ("AllPiecesInLastQuadrant")
                    (forEach Die 
                        combined:True 
                        if:(and
                            ("DieNotUsed")
                            ("IsEndTrack" ("NextSiteFrom" (from) (pips)))
                        )
                        (move Remove (from))
                        (then ("ReplayNotAllDiceUsed"))
                    )
                )
            )
        )
        (regions "FirstQuadrant" P1 (sites {0..5}))
        (regions "FirstQuadrant" P2 (sites {12..17}))
        (regions "LastQuadrant" P1 (sites {12..17}))
        (regions "LastQuadrant" P2 (sites {0..5}))
        (map "StartTrack" {(pair P1 0) (pair P2 12)})
        (hand Each)
    })
    (rules 
        (start {
            (place Stack "Disc1" (handSite P1) count:15)
            (place Stack "Disc2" (handSite P2) count:15)
        })
        
        (play 
            (max Moves 
                ("RollEachNewTurnMove"
                    (if (is Occupied (handSite Mover))
                        (forEach Die 
                            combined:True 
                            if:(and
                                ("DieNotUsed")
                                (is In ("NextSiteFrom" (mapEntry "StartTrack" (mover)) (- (pips) 1)) (sites Mover "FirstQuadrant"))
                            )
                            (move 
                                (from (handSite Mover))
                                (to 
                                    ("NextSiteFrom" (mapEntry "StartTrack" (mover)) (- (pips) 1))
                                    if:("NoEnemyOrOnlyOne" (to))
                                )
                            )
                            (then ("ReplayNotAllDiceUsed"))
                        )
                        (forEach Piece top:True)
                    )
                )
            )
        )
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Buf de Baldriac is a European Tables game played in Spain in the thirteenth century. It is recorded in Alfonso X's Libro de los Juegos.")
        (rules "Played on a Tables board with sockets for the pieces instead of points. Fifteen pieces per player. Three six-sided dice. Players move according to the number on each die by moving one piece the value on one die, another piece the value on another die, and another piece the value on the final die, by moving one piece the value of two of the die subsequently, and a second piece the value of the remaining die, or one piece the value of each die subsequently. Pieces begin off the board. Players must enter all of their pieces onto one half of the board (one player entering in the quadrant to their right and the other player's entering into the quadrant on their left), according to the throws of the dice. Once a player has entered all of their pieces, they move through all of the quadrants of the board toward the quadrant where the opponent entered their pieces. A piece landing on a space with a single opponent's piece sends the opponent's piece back to start, and it must be reentered. Once players enter all of their pieces into the final quadrant, they may bear off their pieces. They can do so by rolling a 6 to move from the sixth point, and so on down to 1. Throughout the game, a player must use the maximum number of moves presented by the dice. The first player to remove all of their pieces wins.
        ")
        (source "Libro de los Juegos 79.")
        (id "1209")
        (version "1.3.10")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Spain, around 1283.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)

