(define "TowardCentre" 
    (not
        (all Sites (sites Centre)
            if:(> (count Steps (to) (site)) (count Steps (from) (site)))
        )
    )
)

//------------------------------------------------------------------------------

(game "The Right Moves" 
    (players 2) 
    (equipment { 
        (board 
            (add
                (remove 
                    (concentric {4 8 16 16 16}) 
                    edges:{
                    {45 29} {44 28} {58 42} {57 51} {40 56} {46 30} {48 32} {49 33} {50 34} 
                    {36 52} {37 53} {38 54} {41 57} {0 3} {3 2} {2 1} {1 0}
                    }
                )
                edges:{{0 2} {1 3}}
            )
        use:Vertex)
        (piece "Counter" Each 
            (if (is In (from) (sites "SecondInnerMostCircle"))
                (or
                    (move Step (to if:(is Empty (to))))
                    (move Hop
                        Rotational
                        (between if:("IsEnemyAt" (between))
                            (apply (remove (between)))
                        )
                        (to if:(is Empty (to))
                        )
                    )
                )
                (or
                    (move Step (to if:(and ("TowardCentre") (is Empty (to)))))
                    (move Hop
                        Rotational
                        (between if:("IsEnemyAt" (between))
                            (apply (remove (between)))		
                        )
                        (to if:(and ("TowardCentre") (is Empty (to))))
                        (then
                            (if (can Move
                                    (if (is In (last To) (sites "SecondInnerMostCircle"))
                                        (hop
                                            (from (last To))
                                            Rotational
                                            (between if:("IsEnemyAt" (between)))
                                            (to if:(is Empty (to)))
                                        )
                                        (hop
                                            (from (last To))
                                            Rotational
                                            (between if:("IsEnemyAt" (between)))
                                            (to if:(and ("TowardCentre") (is Empty (to))))
                                        )
                                    )
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        (piece "DoubleCounter" Each)
        (regions "SecondInnerMostCircle" (sites {4..11}))
    }) 
    
    (rules 
        (start {
            (place "Counter1" (sites {48..50 32..34 16..18 24..26 40..42 56..58}))
            (place "Counter2" (sites {44..46 28..30 12..14 52..54 36..38 20..22}))
        })
        (play 
            (if ("SameTurn")
                (or
                    (if (is In (last To) (sites "SecondInnerMostCircle"))
                        (move Hop
                            (from (last To))
                            Rotational
                            (between 
                                if:("IsEnemyAt" (between))
                                (apply (remove (between)))
                            )
                            (to if:(is Empty (to)))
                        )
                        (move Hop
                            (from (last To))
                            Rotational
                            (between 
                                if:("IsEnemyAt" (between))
                                (apply (remove (between)))
                            )
                            (to if:(and ("TowardCentre") (is Empty (to))))
                        )
                    )
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        (end (if (no Moves Next) (result Next Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        }
    )
    
    (graphics {
        (player Colour P1 (colour Blue))
        (player Colour P2 (colour Red))
        (board Background image:"Square" fillColour:(colour LightGrey) edgeColour:(colour LightGrey) scale:1.2)
        (no Sunken)
        (board Colour InnerEdges (colour White))
        (board Colour OuterEdges (colour White))
        (show Symbol "Disc" (sites Board) fillColour:(colour White) edgeColour:(colour White) scale:0.75)
    })
    
)
