(game "Jeu Militaire" 
    (players 2) 
    (equipment { 
        (board 
            (rotate 90 ("ThreeMensMorrisGraphWithLeftAndRightTriangles"))
            use:Vertex
        )
        <Version:hand>
        
        (piece "Pawn" P1 ("StepToEmpty" <Army>))
        (piece "Pawn" P2 N ("StepToEmpty" <Tower>))
        (regions "Home" P2 (sites {"B1" "C2" "A2"}))
        (regions "Home" P1 (sites {"B2"}))
    }) 
    
    (rules 
        <Version:start>
        
        <Version:play> 
        
        (end {
            (if <EndCondP2> (result P2 Win))
            <Move:limit>
            (if (is In (where "Pawn" P1) (sites P2)) (result P1 Win))
            //   (if 
                //   	(= 
                    //   		(row of:(where "Pawn" P1)) 
                    //   		(min (results from:(sites Occupied by:P2) to:0 (row of:(from))))
                //   	) 
                //   	(result P1 Win)
            //   )
        }) 
    )
)

//------------------------------------------------------------------------------

(option "Direction Tower" <Tower> args:{ <direction> }
    {
    (item "All directions"   <(directions {Forwards Rightward Leftward})> "The towers can move in sideway and forwards (including diagonal and orthogonal forward).")* 
    (item "No Sideways"   <Forwards>         "The towers can move forwards (including diagonal and orthogonal forward).") 
    (item "Only Forward"   <Forward>          "The towers can move only forward.") 
    (item "Forward and Sideways" <(directions {Forward Rightward Leftward})> "The towers can move forward and sideway.") 
})

(option "Direction Army" <Army> args:{ <direction> }
    {
    (item "All directions" <>   "The army can move in all the directions.")** 
    (item "Orthogonal"  <Orthogonal> "The towers can move only orthogonally.") 
})

(option "P2 Ending Conditions" <EndCondP2> args:{ <EndCondP1> }
    {
    (item "No Moves P1" <(no Moves P1)>   "The army can not move.")** 
    (item "No Moves P1 after Moving three pieces"  <(and (= (count MovesThisTurn) 2) (no Moves Next))> "The army can not move and all towers moved.") 
})

(option "Version" <Version> args:{ <hand> <start> <play>}
    {
    (item "Lucas" 
        <> 
        <(start {
            (place "Pawn1" (sites P1))
            (place "Pawn2" (sites P2))
        })
        >
        <(play (forEach Piece))>   
        "The game is played according to the rules of Lucas with the pieces already on the board."
    )**
    
    (item "Martin Gardner"  
        <(hand P1)> 
        <(start {
            (place "Pawn1" "Hand1")
            (place "Pawn2" (sites P2))
        })
        >
        
        <
        phases:{
        (phase "Placement" P1 
            (play 
                (move 
                    (from (handSite P1)) 
                    (to (sites Empty))
                )
            ) 
            (nextPhase "Movement")
        )
        ("PhaseMovePiece" "Movement")
        }
        >
        "The game is played according to the rules of Martin Gardner. The army can be placed anywhere."
    ) 
    
    (item "Martin Gardner (improved)"  
        <(hand P1)> 
        <(start {
            (place "Pawn1" "Hand1")
            (place "Pawn2" (sites P2))
        })
        >
        
        <
        phases:{
        (phase "Placement" P1 
            (play 
                (move 
                    (from (handSite P1)) 
                    (to (difference (sites Empty) (sites {"B2"})))
                )
            ) 
            (nextPhase "Movement")
        )
        ("PhaseMovePiece" "Movement")
        }
        >
        "The game is played according to the rules of Martin Gardner with a slightly difference. The army can be placed anywhere except in the middle of the towers."
    ) 
    (item "Schuh"  
        <(hand P1)> 
        <(start {
            (place "Pawn1" "Hand1")
            (place "Pawn2" (sites P2))
        })
        >
        <
        phases:{
        (phase "PlacementP1" P1 
            (play 
                (move 
                    (from (handSite P1)) 
                    (to (difference (sites Empty) (sites {"B2" "B3"})))
                )
            )  
            (nextPhase Mover "MovementP1")
        )
        (phase "MovementP1" P1 
            (play (forEach Piece)) 
        )
        (phase "MovementP2" P2 
            (play 
                (forEach Piece 
                    (if (!= 1 (state at:(from))) 
                        (move Step (directions {Forwards Rightward Leftward}) (to if:(is Empty (to)))
                            (then
                                (if (< (count MovesThisTurn) 2) 
                                    (and
                                        (moveAgain)
                                        (set State at:(last To) 1)
                                    )
                                    (forEach Site (sites Occupied by:P2)
                                        (set State at:(site) 0)
                                    )
                                    
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        >   
        "The game is played according to the rules of Lucas, except that Schuh suggested that White has to move his 3 pieces (in any order) at each turn."
    ) 
})

(option "Move Limit" <Move> args:{ <limit> }
    {
    (item "No move limit" <>          "")** 
    (item "12"   <(if (= (count Moves) 12) (result P1 Draw))> "After twelve moves if no winner, the game is a draw.") 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Lucas (Described)" {
        "Direction Tower/All directions" "Direction Army/All directions" "P2 Ending Conditions/No Moves P1" "Version/Lucas" "Move Limit/No move limit"
    })*
    
    (ruleset "Ruleset/Gardner (Suggested)" {
        "Direction Tower/All directions" "Direction Army/All directions" "P2 Ending Conditions/No Moves P1" "Version/Martin Gardner" "Move Limit/No move limit"
    })
    
    (ruleset "Ruleset/Schuh (Suggested)" {
        "Direction Tower/All directions" "Direction Army/All directions" "P2 Ending Conditions/No Moves P1 after Moving three pieces" "Version/Schuh" "Move Limit/No move limit"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The Jeu Militaire is a game documented since the nineteenth century in France. It was reported by several sources as being played in the French military.")
        (aliases {"French Military Game" "Hare and Hounds"})
        (useFor "Ruleset/Lucas (Described)" (rules "The board consists of three instersecting lines, with diagonals drawn in the square formed. On opposite ends of the square, the central line is extended beyond the square and lines drawn from the adjacent corners to the end of this line, forming two triangles at opposite ends. One player takes the part of three white towers, the other the part of a single black army piece. First the army moves their piece, then the towers move one of their pieces, play alternating thereafter until the game is ended. A tower piece may move one step along a marked line in any forward or sideways direction. Tower pieces cannot move backwards, diagonally or otherwise, towards the end of the board from which they started. The army may move one step in any direction along a marked line. The army wins by passing the towers and reaching the end of the board from which they started. The towers win by trapping the army so that it cannot move in its turn."))
        (useFor "Ruleset/Lucas (Described)" (source "Lucas 1887: 402-404."))
        (useFor "Ruleset/Lucas (Described)" (id "387"))
        (useFor "Ruleset/Gardner (Suggested)" (rules "The player with the single piece may place their piece on any empty spot at the beginning of the game. "))
        (useFor "Ruleset/Gardner (Suggested)" (source "Garnder, Martin. 1963. \"Mathermatical Games.\" Scientific American 209(4): 125-126."))
        (useFor "Ruleset/Gardner (Suggested)" (id "591"))
        (useFor "Ruleset/Schuh (Suggested)" (rules "The game is played according to the rules of Lucas, except that Schuh suggested that White has to move his 3 pieces (in any order) at each turn."))
        (useFor "Ruleset/Schuh (Suggested)" (id "2881"))
        (source "Lucas 1887: 402-404.")
        (id "387")
        (version "1.3.10")
        (classification "board/hunt")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })
    
    (ai
        "Jeu Militaire_ai"
    )
)
