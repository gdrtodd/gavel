(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

//------------------------------------------------------------------------------

(game "Shodra"
    ("TwoPlayersNorthSouth") 
    (equipment {
        (board (square 8))
        (hand Each size:5)
        ("ChessKing" "King_noCross")
        ("ChessRook" "Rook")
        (piece "Camel" Each
            (move Hop Diagonal 
                (between if:True) 
                (to 
                    if:(or 
                        (is Empty (to)) 
                        ("IsEnemyAt" (to))
                    ) 
                    (apply (remove (to)))
                ) 
            )			
        )
        ("ChessKnight" "Knight")
        ("ChessPawn" "Pawn"
            (if (is In (from) (sites Start (piece (what at:(from)))))
                ("DoubleStepForwardToEmpty")
            )
            (then ("PromoteIfReach" (sites Mover "Promotion") "Dog"))
        )
        (piece "Dog" Each ("StepToNotFriend" Diagonal))
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Camel1" {"C1" "F1"}) (place "Dog1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Camel2" {"C8" "F8"}) (place "Dog2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        (play 
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        (end 
            (if (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                {
                (if (no Pieces Next "Pawn") (result Mover Win))
                (if (!= (count Pieces Next "Pawn") 0) (result Mover Draw))
                }
            ) 
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (rules "Dessa regler följdes i Mongoliet 1913. >Spelade som schack. Drottningen (känd som hundar i det här spelet) kan bara flytta en ruta diagonalt. Biskopen (här, en kamel) hoppar diagonalt till den andra rutan. En spelare måste först fånga alla motståndarens bönder innan schackmatt för att vinna.")
        }
    )
    
    (graphics {
        (piece Scale "Pawn" 0.825)
        (show Check "King_noCross")
        (board Style Chess)
        (board Placement scale:0.8 offsetY:-0.25)
    })
    
    (ai
        "Shodra_ai"
    )
)

